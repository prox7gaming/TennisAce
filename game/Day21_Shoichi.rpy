label Day21_Shoichi:
    window hide
    scene June1 with fade
    play sound "music/windchimes.ogg"
    show blossoms movie
    $ renpy.pause(5.5)
    play music "music/night.ogg" fadein 5.0
    scene SCorridorN with fade
    window show
    $ date = "day21"
    "By the time I left that last class display, the sun had already completely set and it was too dark to see without the hallway lights."
    "Most students and guests alike were headed to the outside of the school buildings as the last few remaining displays began to shut down."
    "I spent all of today on my own."
    "I actually managed to see Shoichi getting dragged around by the arm once a few hours ago."
    "I guess Hitoka-chan was serious about not letting him go."
    "Well, this is what he gets for ignoring his sister for so long."
    "... Still, I wish I could be spending time with him right now."
    scene SGateN with fade
    "Exiting the building, I see a lot of people huddled around the bonfire."
    "They are stacking the logs all neatly in a squared shape, all of them set up very precisely."
    "I guess the point is for it to look aesthetic for the photos?"
    "Also, is it bigger than last year's or is it just my imagination?"
    "I guess Shoichi did a really good job of clearing the budget for the festival, huh?"
    "Looking around, there are definitely a lot of people here today."
    "I wonder if there are more guests this year than we had last year? Far as I know no one makes a census of that so I don't think anyone knows."
    sa 1 c laugh "\"Ah. [povFirstName]!\""
    show sa 1 c laugh at fdis, five
    "I hear my name being called and immediately turn around, seeing the figure of a tall bunny hopping towards me with a big smile."
    mc 1 c talk "\"Hey there, Saya.\""
    sa 1 c smile "\"I haven't seen you since Monday. What's up?\""
    mc 1 c considerate "\"Oh. Not much. I've just been going around the festival looking at anything that seemed interesting. What about you?\""
    sa 1 c talk "\"I helped out a lot at my class' café so I didn't have as much free time as I would have liked.\""
    mc 1 c considerate "\"That's too bad. I was free the whole time but... honestly, I didn't have as much fun as I thought I would.\""
    sa 1 c "\"Really? How come?\""
    mc 1 c wince "\"Well... everyone else was busy and I kinda got tired of going around alone.\""
    sa 1 c shock "\"Whoa. Are you okay? That certainly doesn't sound like you.\""
    mc 1 c considerate "\"Even I can want company every now and again. It's not weird.\""
    "... Although it was a specific person's company that I missed the most."
    sa 1 c "\"Did you come outside to watch the bonfire dance? Or are you taking part?\""
    mc 1 c curious "\"Nah. I don't really have anyone to dance with. I just didn't really have a reason to stay indoors. Unless you'd like to dance with me.\""
    sa 1 c considerate "\"Sorry, I already have someone to dance with.\""
    mc 1 c smile "\"Oh, right. Are you going to dance with Kei-kun? You did dance with him last year.\""
    sa 1 c laugh "\"Nah. I'm gonna dance with this guy I've been kind sorta maybe flirting with for a while now. Besides, Kei-kun is with his band.\""
    mc 1 c shock "\"Wait wait wait, a guy you've been... You have a {i}date{/i}?!\""
    show sa 1 c shock at fdis
    $ renpy.pause(1.0)
    show sa 1 c pout at fdis
    "As soon as those words left my mouth, I knew I had screwed up."
    mc 1 c considerate "\"I-I mean, wow, good for you. You have a date!\""
    sa "\"Nice try!\""
    play sound "music/punch.ogg"
    mc 1 c wince "\"Oof!\"" with hpunch
    "For my indiscretion, I receive a swift punch to the gut."
    mc 1 c considerate "\"S-sorry...\""
    sa 1 c sigh "\"Why do you even have to be so surprised? It's not like I've never dated before. I've had boyfriends you know.\""
    mc 1 c considerate "\"Y-yeah, but... You've been single since you and Morisaki-senpai broke up. I kinda expected you to stay that way.\""
    sa 1 c pout "\"It's not like I was single because no guys hit on me. I just wasn't interested in any of them.\""
    "To be perfectly honest, I have a hard time believing any guy would hit on her."
    "Who would ever want such a violent woman as a girlfriend?"
    "I'd probably be completely emasculated if I ever dated someone like her."
    sa 1 c haughty "\"I guess you'll just have to watch us dance, huh?\""
    mc 1 c sigh "\"Oh sure. Relish my misery. That is so nice of you.\""
    sa 1 c happy "\"If you didn't want me to then you shouldn't have been an ass.\""
    "For fuck's sake, I just blurted it out, it's not like I wanted to offend you!"
    "Devil woman."
    mc 1 c considerate "\"I'll just stand by the sidelines and I'll watch everyone else dance. You and your mystery guy, Shoichi and Hitoka-chan...\""
    sa 1 c "\"... Hey, [povFirstName]-kun?\""
    mc 1 c "\"Yeah?\""
    sa 1 c bored "\"I'm gonna ask you a question and... whatever you say, I'll believe you, okay?\""
    mc 1 c wince "\"Uhm... okay?\""
    "Why are you acting so awkward all of a sudden?"
    sa "\"Are you and Sho-chan dating?\""
    play sound "music/heartbeat.ogg"
    "?!"
    "I immediately freeze."
    "I wasn't expecting to get asked this... and point blank too."
    "My mind goes blank and for a second a have a hard time even absorbing what I was just asked."
    mc 1 c wince "\"W-why are you asking me that? That's such a weird question.\""
    sa 1 c considerate "\"It's just that you two have been awfully close lately so... you know.\""
    "..."
    $ sholove = 0
    menu:
        "Tell the truth":
            $ sayaquestion = "Truth"
            $ sholove += 1
            "I..."
            "To be honest, I have no reason to hide this from her."
            "Saya is my oldest friend, right up there with Shoichi."
            "And he even said it himself that she already knew he had feelings for me for a while."
            "I... of all the people I know, I think she's the most likely to be supportive of us."
            mc 1 c worried "\"We... we are, yeah.\""
            show sa 1 c laugh at fdis, jumping
            sa "\"Really?! Oh, that's so great. I'm so happy for you guys!\""
            play sound "music/fabric.ogg"
            "She suddenly tackles me, wrapping her arms around me in a tight, bone-crushing hug."
            mc 1 c wince "\"Ow ow ow ow, I get it, I get it. Please let go of me.\""
            sa 1 c think "\"Oh, sorry.{nw}"
            show sa 1 c laugh at fdis
            extend " Still, that's great news! I've known he's had feelings for you for so long but I never thought anything would come out of it.\""
            mc 1 c considerate "\"I only found out about it recently... And I didn't even know I liked guys until then. To be honest, I'm still confused about the whole thing and not so sure about how it... works.\""
            sa 1 c smile "\"You're overthinking it. It's not that different from normal. Falling for someone is always the same. The only difference is what they have between their legs.\""
            mc 1 c wince "\"D-do you really have to put it that way?\""
            sa 1 c think "\"What? It's true.\""
            "You're sooo not lady-like at all."
            sa 1 c laugh "\"Oh man, I have to go congratulate him later!\""
            mc 1 c fsmile "\"Aren't you a bit too excited about this?\""
            sa 1 c smile "\"Oh come on, if the two of you are dating then Shoichi must be really happy right now. And in such good timing too considering his team lost this weekend.\""
            sa 1 c happy "\"You're probably the only reason he can be cheerful right now.\""
            "..."
            "I hadn't thought of that."
            mc 1 c considerate "\"It's not all good. I kept wishing I could spend time with him during the festival but he's been so busy. We've only seen each other two or three times since the weekend.\""
            sa 1 c think "\"Ah. That {i}is{/i} a bummer. I guess you must be really upset that Hitoka-chan is dragging him around, huh?\""
            mc 1 c considerate "\"Yup.\""
            sa 1 c smile "\"Still, I wish you guys all the best. I hope it all works out between you two."
            show sa 1 c doom at fdis
            extend " And I swear, if you do the same thing to him that you've done with your ex-girlfriends I will castrate you.\""
            mc 1 c shock "\"R-roger that!\""
            sa 1 c laugh "\"Anyway, I have to go. Talk to you later!\""
            show sa 1 c at offscreenright with moveoridis
            "..."
            "Scary! Saya-chan, you're too scary!" with hpunch
        "Deny it":
            $ sayaquestion = "Deny"
            "I..."
            "I shouldn't have any reason to hide this from her."
            "I... I know Saya-chan would be nothing if not supportive, but..."
            "I'm just... not ready to tell anyone."
            "I'm scared... even though I know she'll be happy for us, I'm scared..."
            mc 1 c worried "\"N-no. Of course we're not dating, don't be insane. Shoichi's a guy.\""
            sa 1 c wry "\"Oh... I see. Sorry to ask such a weird question.\""
            mc 1 c considerate "\"Jeez, where do you get these kinds of ideas? You've been reading too much BL manga.\""
            sa 1 c considerate "\"Ehehe, you're probably right, sorry. I just kept seeing you two being so close together and I kinda started wondering.\""
            show sa 1 c bored at fdis, five
            "..."
            "Oh God, the mood is so awkward now."
            "How do we even rescue the conversation after that?"
            sa 1 c considerate "\"B-by the way, did you see Kei-kun at all tonight? He was pretty down.\""
            mc 1 c wince "\"R-really? What happened?\""
            sa 1 c bored "\"Apparently Ichigo-chan got sick and she couldn't sing so they had to cancel their show.\""
            mc 1 c shock "\"Their band was putting up a show?!\""
            sa 1 c pout "\"How did you not know that?! Don't you pay attention to your friends?\""
            mc 1 c wince "\"H-he never told me that, how was I supposed to know?!\""
            sa 1 c sigh "\"You're hopeless.\""
            "But... but... but I didn't do anything!"
            sa 1 c wry "\"Anyway, I've gotta go now. I still have to find my date in the middle of all these people. Talk to you later.\""
            mc 1 c considerate "\"Yeah... see ya.\""
            show sa 1 c at offscreenright with moveoridis
            "..."
            "Man, that conversation was all kinds of painful."
    play sound "music/fire.ogg"
    "The bonfire is lit and the people around it all take a step back to avoid standing too close to the flames."
    "I watch the orange flames crackling and dancing around high in the air."
    "The heat against my face is pretty comfortable, even if it's a little bright."
    "Little by little, people start forming a wide circle around the fire, separating themselves in pairs."
    "Be they friends, lovers, whatever, there are lots of people beginning to dance together."
    "It's something I've never really taken part in during all my years of school."
    "I never really saw the point. I always thought it was a silly little thing."
    "As people dance around the bonfire, going in circles, I spot Shoichi and Hitoka dancing."
    "I wave at them and the two seem to spot me too."
    "Hitoka-chan waves enthusiastically at me."
    "Shoichi on the other hand, looks at me with a scrunched up face."
    "I can't really tell what he's thinking."
    "Maybe he also wishes he were dancing with me?"
    "It'd be nice if that were the case."
    "They disappear behind the bonfire again."
    "It's weird how entrancing and weirdly rhythmic this whole thing is, even though people are all doing their own thing and no pairs are synced up with each other."
    "It almost looks like a wave going to and fro."
    "I have to admit, it's kinda fun to watch."
    "When the circle goes around again, I see Shoichi and Hitoka have disappeared."
    "I wonder to myself what happened to them when I see the two walking my way."
    "Did they give up on dancing already?"
    "I know Shoichi has two left feet but he can't have been {i}that{/i} bad that she just called it quits."
    show s 1 c considerate at fdis, three
    show hi 1 u smile at fdis, seven
    with dissolve
    mc 1 c talk "\"What are you guys doing? Gave up on dancing already?\""
    hi "\"I already had my fun. Besides, I'm not the one this idiot wants to dance with.\""
    mc 1 c worried "\"H-huh?\""
    s 1 c wry "\"Sorry, Hitoka. I promise I'll make it up to you later.\""
    hi "\"Whatever. Go dance with him if it's that important to you. Just don't blame me if he's not as good a dancer as I am.\""
    "She chuckles, poking him in the ribs with a haughty smile on her face."
    mc 1 c shockb "\"W-wait, what are you guys talking about?\""
    s 1 c happy "\"[povPetName], would you dance with me?\""
    mc 1 c flustered "\"W-what? Seriously? I-I know it's not uncommon for friends to dance together b-but there are very few {i}guys{/i} dancing together.\""
    "The underlying question is pretty clear."
    "\"Are you sure you want to do it in front of all these people?\""
    "Speaking of what I personally want... yeah, I'd love to dance with him."
    "But... what if people think something's up because we did?"
    "Hell, I need to stop overthinking things..."
    "How did Hitoka even agree to this? There's no way he told her about us... otherwise she wouldn't be in such a good mood."
    s 1 c wry "\"I don't mind if you don't.\""
    "The look on his face... his eyes looked at me almost pleadingly."
    "Like he were to say \"Please... dance with me.\""
    "Shoichi raised his hand, offering his open palm to me."
    "I swallowed a big chunk of saliva, trying to work up the courage to take it."
    hi "\"Oh, stop dawdling already. Otherwise, the dance will be over and you'll have gotten nowhere!\""
    play sound "music/slap.ogg"
    mc 1 c shock "\"Waaaah!\"" with hpunch
    "She gives me a strong slap on the back that makes me jump forward."
    show cg8_1 with Dissolve(1.0)
    play music2 "music/BGM/The Meaning of Tears.ogg" fadein 10.0
    "Shoichi smiles warmly at me, his eyes silently encouraging me to accept his offer."
    "I can't believe he actually asked her to let us dance together... he's such an idiot."
    "What if that had clued her in to us dating?"
    "Really, what a reckless idiot..."
    mc 1 c avoidb "\"... Are you sure about this?\""
    s2 "\"As sure as I'll ever be.\""
    "God, his eyes are so beautiful with the light of the bonfire shining on them."
    "I raise my hand to take his, deciding to forget about being cautious."
    "I'll just do what I want to do right now."
    "After all, this is our last year... this will be my only chance to do this with him while we're still students together."
    show cg8_3 with Dissolve(1.0)
    "I grasp Shoichi's hand with my own."
    "His hand is soft and warm and even a little clammy."
    "I can feel my heart racing in my chest."
    "God, this is so embarrassing."
    "Still... I'm okay with that for now."
    s2 "\"Heh. Thanks for indulging me.\""
    mc 1 c zero "\"I think I should be saying the same.\""
    "I laugh, suddenly feeling really bold."
    "Shoichi smiles at me, his bright green eyes shining like gems, all their warm affection directed at me."
    s2 "\"Come on, let's join the circle.\""
    show cg8_2 with Dissolve(1.0)
    "Shoichi leads me towards the circle of students dancing together."
    "My ears twitch and my whole body shudders under this sudden heat."
    "Still, the heat of the fire gently envelops me, its bright light tinting the ground beneath us orange."
    "We slowly dance around the circle."
    "I try my best not to be awkward but it's no secret to anyone that I'm not a dancer."
    "Despite tripping a few times and making total fools of ourselves, the two of us laugh and smile the whole time."
    "Shoichi wraps an arm around my waist, squeezing me tightly and fondly."
    "I enjoy the feeling of his hands on me far more than I thought I would."
    "It's weird. Over this past week, I've had many times where I had doubts or felt confused about my feelings."
    "I felt uneasy, uncertain, and worried about how things might end up between us."
    "But every time we get close like this, I forget all those thoughts and just... enjoy myself."
    "Far more than I felt I could doing things like this."
    "Even if it's simple and silly, spending time alone together makes me happier than I thought it could."
    "I'm glad he asked me to dance, and I'm glad I agreed to."
    s2 "\"You look pretty cute with that look on your face.\""
    "Shoichi leans his snout close to my ear and whispers to me, a big, handsome smile on his face."
    "I feel an urge to kiss him then and there but I stop myself."
    "It's not something I should do in public like this."
    "... I get the feeling that every time he kisses me, I become more addicted to that feeling."
    "God, I want us to be alone so much right now."
    "It's so weird to suddenly be feeling that way."
    mc 1 c zero "\"Thanks. You look pretty handsome too.\""
    s2 "\"Heh. I'm glad you think so. Yours is the only opinion that matters to me on that front.\""
    "My face feels a bit flushed at those words. My ears twitch a little from the embarrassment."
    mc 1 c zero "\"Smooth talker.\""
    s2 "\"Pfft, I wish. Maybe if I were I would have managed to get with you earlier.\""
    mc 1 c zero "\"Hahaha, somehow I just don't see that happening.\""
    s2 "\"Hey, it's not like you're so smooth either. And you're also a terrible dancer.\""
    mc 1 c zero "\"Tch, the same goes for you!\""
    "I subtly kick him on the ankle, making him wince and laugh at the same time."
    s2 "\"Ow! You're kicking me now? You're lucky we're in public otherwise I'd make you pay for that.\""
    mc 1 c zero "\"Hahaha. I'm not afraid to take you on, big guy.\""
    s2 "\"Heh. I'll keep that in mind.\""
    "Shoichi squeezes my hand tighter."
    "I can barely feel the warmth of his body amid the heat of the fire, but nevertheless it's still there."
    "It's a very comfortable, nostalgic feeling."
    "Now that I think about it, he's always looked for an excuse to hold or hug me whenever he could."
    "When I think about it, those memories are far more precious to me than I thought they would be."
    "I think I might be falling for him pretty hard."
    "It scares me just how easily that happened."
    "Stupid handsome Shoichi..."
    stop music2 fadeout 2.5
    scene SkyN with fade
    show hour with dissolve
    $ renpy.pause(1.0)
    hide hour with dissolve
    "I barely noticed time passing by."
    "In fact, the dance was over all too quickly."
    "Part of me feels like I wasn't able to enjoy it at all."
    "I'm walking home, quietly watching the stars above, humming an old song while I do so."
    s 1 c smile "\"You seem to be in a good mood.\""
    "Shoichi speaks up from beside me."
    "I feel his hand brushing up against mine and the swooshing sensation of his tail wagging behind me."
    "Of course, I'm dimly aware that mine is wagging just as much as his."
    "It feels... peaceful."
    mc 1 c happy "\"I am.\""
    scene HouseFrontN
    show s 1 c smile at fdis, five
    with fade
    "We reach my house all too soon."
    "Suddenly it feels like these walks home that I used to find so dull and long have started to go by pretty quickly."
    "I wish they were even longer..."
    mc 1 c smile "\"Thanks for walking me home.\""
    s 1 c smile "\"You don't have to thank me. It was my pleasure after all.\""
    mc 1 c considerate "\"I hope Hitoka-chan wasn't too miffed about it.\""
    s 1 c think "\"She lives in the opposite direction... and so far away that she needs to take a car to get there. She's my sister and I love her but there's no way I'm walking for an hour and twenty minutes to take her home.\""
    mc 1 c "\"Did you call a cab for her?\""
    s 1 c considerate "\"Yeah. Besides, if I walked her home I would have no excuse not to come inside. I still feel... a bit awkward about seeing my mom.\""
    mc 1 c wince "\"Why?\""
    s 1 c wry "\"I don't know. Part of me feels guilty for not going with her during the divorce. I haven't been around to see her in a while.\""
    mc 1 c worried "\"That's the first time I've heard of this. Why did you never tell me?\""
    s 1 c considerate "\"I didn't want to worry you.\""
    s 1 c smile "\"But then again, after seeing what you did during Dad's birthday party, I think it's better if I explain my reasons to you. Otherwise you might just show up with my mom out of the blue one day.\""
    mc 1 c wince "\"Ugh. You're never going to let me live that down, are you?\""
    s 1 c happy "\"Not a chance.\""
    show s 1 c flattered at fdis
    play sound "music/chu.ogg"
    "He takes a quick look around to see if there is anyone nearby before giving me a quick peck on the lips."
    s 1 c wry "\"Man, I wish I could stick around a little longer.\""
    mc 1 c considerate "\"So do I.\""
    mc 1 c smile "\"I... kinda have an idea. Could you ask your dad if he'd be okay with you spending a night here during the weekend? He's allowed that before so I don't see why he wouldn't now.\""
    s 1 c think "\"I can try. He's been in a good mood lately so now might be a good time to ask.\""
    mc 1 c happy "\"That's great. Mom will be travelling to a neighboring city to help on a case so it'll just be us two and Aki.\""
    s 1 c smile "\"Not like I have any problem spending time with your mom. Misaki-san seems to like me a lot too.\""
    mc 1 c considerate "\"I wonder how much she'd like you if she knew you were my boyfriend.\""
    s 1 c considerate "\"That... is a fair consideration. I have no idea.\""
    s 1 c wry "\"Still... It's nice to hear it. You calling me \"boyfriend\" like it's no big deal.\""
    mc 1 c "\"Yeah... I know I can be awkward and a little slow at dealing with things but... I've been feeling a lot more comfortable with it these past few days.\""
    mc 1 c happy "\"But then again, it's only been a week so it's not like I've been super slow to get used to it.\""
    s 1 c smile "\"To be honest, things have been going much smoother than I expected.{nw}"
    show s 1 c considerate at fdis
    extend " A part of me totally thought you'd have broken up with me by now.\""
    mc 1 c worried "\"That's not a nice thought. Don't think things like that.\""
    s 1 c considerate "\"I try not to. It just comes to mind when I'm distracted.\""
    s 1 c wry "\"You come to my mind a lot when I'm distracted.\""
    play sound "music/heartbeat.ogg"
    mc 1 c flustered "\"Sho...\""
    s 1 c laugh "\"I should probably get going. If I stick around too much I won't want to leave. Hell, I already don't want to.\""
    mc 1 c smile "\"That's fair. I hope I'll get to see you again tomorrow.\""
    s 1 c smile "\"You bet.\""
    show s 1 c smile at fdis, offscreenleft with moveoledis
    "I watch him disappear into the nearby turn, silently wishing he could stay a little longer."
    "God, is this really what falling for someone is like?"
    "It's such a weird feeling."
    "... But I still like it."
    stop music fadeout 2.5
    $ date = None
    jump Day22_Shoichi
