label Day18_Jun:
    window hide
    scene May30 with fade
    play sound "music/windchimes.ogg"
    show blossoms movie
    $ renpy.pause(5.5)
    play music "music/crowd01.ogg" fadein 5.0
    scene SCorridor with fade
    window show
    $ date = "day18"
    $ junlove = 0
    "I stand around with my back against the wall, impatiently tapping my feet on the floor."
    play sound "music/phonebeep.ogg"
    "I pull out my phone to look at the time, realizing it's already been fifteen minutes since the agreed upon time."
    "2:30. That's when Jun's \"shift\" was supposed to end."
    "We had agreed through messages yesterday that we'd go around together just the two of us since the other all said they were too busy to leave."
    "And yet there is still no sign of the tiger."
    "... I don't want to go inside the classroom and get yelled at by either Kyoko or Ayako for not helping around so I chose to wait outside, a little ways away from our class."
    "Still, I've kept my eyes peeled on the door the whole time and nothing."
    play sound "music/disappointment.ogg"
    "How come every time I set something up with Jun I always end up waiting?"
    "Maybe I should start to schedule with him fifteen minutes before I actually show up so I won't have to wait so long."
    "... But then, knowing how he is, he'd be likely to arrive right on time if I did that and then complain that I was late."
    "I sigh, rubbing my forehead as I feel a tiny bit of frustration building up."
    "I just want to go around the festival and have fun with a friend, not wait around the door like a glorified bouncer."
    play sound "music/slidingdoor.ogg"
    "My ears twitch when I hear the sound of the door opening."
    show j 1 ch think at fdis, five with dissolve
    "I turn my head to look and sure enough see Jun looking around the hallway for me."
    mc 1 c smile "\"Over here!\""
    show j 1 ch shock at fdis
    "I call out to him, waving with my left arm. A few heads among the crowds walking around the hall turn to me due to the sudden noise but I decide to ignore them."
    "Like I imagined, the place is much, much more packed than it was yesterday."
    "Part of me is really worried how Jun is going to react to the whole thing."
    "Since he started his shift first thing in the morning, there weren't that many people around when he arrived."
    "I hope it hasn't been too hard on him today."
    play music2 "music/BGM/Little by Little I Walk.ogg" fadein 5.0
    j 1 ch happy "\"[povFirstName]-san, good afternoon!\""
    "He walks up to me with a cheerful smile on his face."
    "I can see his tail swishing around behind him."
    "Guess even if he's calm right now, he can still feel all the people around."
    "I make a mental note to try and avoid any really crowded areas."
    show j 1 ch smile at fdis
    mc 1 c smile "\"Good afternoon. How was work?\""
    j 1 ch wince "\"Blegh... it was hard.\""
    "Well, I figured that much. No matter what I just can't picture you doing well as a waiter."
    "Still, that'd be way too cruel to say out loud so I'll keep it to myself."
    j 1 ch watch "\"By the way, you're not wearing your uniform today?\""
    mc 1 c considerate "\"Nah. Most students only wear it during the first day so they are allowed inside. We have teachers standing by the gates to make sure only students and staff show up then.\""
    j 1 ch think "\"But Mizoguchi-san wasn't wearing a uniform.\""
    show j 1 ch watch at fdis
    mc 1 c wry "\"Yeah... I have no idea how she got in. Probably brought her other clothes in her bag and then switched out of her uniform.\""
    j 1 ch wince "\"T-that's a lot of work just to avoid using a uniform.\""
    mc 1 c smile "\"Not that I can blame her. The uniform is too stuffy and uncomfortable. And with lots of people walking around the entire time, it can get a bit hot.\""
    mc 1 c considerate "\"Especially since we're just a few days away from the start of summer and the temperatures are already going up a bit.\""
    j 1 ch watch "\"I don't really mind wearing the uniform.\""
    show j 1 ch watch at fdis
    mc 1 c smile "\"By the away, why don't you switch out of your waiter uniform? It has got to be an oven inside that thing.\""
    j 1 ch wince "\"N-no. I like it!\""
    "The tiger hugs his arms close to his body, taking a step away from me and shooting me a weird look. His tail lashes behind him."
    mc 1 c sigh "\"You have beads of sweat rolling down your face and stains on stomach and chest.\""
    j 1 ch considerate "\"T-that's just because it was really hot inside the restaurant.{nw}"
    show j 1 ch think at fdis
    extend " Ayako-san said she was going to have a bunch of wall-mounted fans installed this afternoon to make things a little less unbearable for the guests.\""
    "For the guests? They only stick around for thirty minutes at most. You should be worried about your classmates who have to work inside that scorching heat for most of the day."
    "Devil woman."
    show j 1 ch watch at fdis
    mc 1 c sigh3 "\"Why are you even that worried about continuing to wear this thing? Is it because you have another shift in a few hours and don't want to have to get changed again.\""
    j 1 ch considerate "\"Not really. It's just...\""
    mc 1 c "\"Just?\""
    j 1 ch wince "\"W-well, I won't have another chance to wear this again once the festival is over, will I? And it looks so good too!\""
    mc 1 c "\"Can't you just take it home with you and wear it around whenever you want?\""
    j 1 ch considerate "\"[povFirstName]-san, we weren't given this. This is a rental.\""
    mc 1 c worried "\"O-oh...\""
    "I... guess I can understand a little better why he's being so stubborn."
    "I mean... he's still being annoyingly stubborn, but..."
    "It's a bit more acceptable, all things considered."
    "... I think."
    j 1 ch smile "\"Anyway, how about we go around checking the other classes? I really want to see what they've done!\""
    mc 1 c smile "\"Sure. How about I take you to some of the ones I've already seen and quite liked?\""
    j 1 ch gentle "\"Yes please. Lead the way!\""
    play sound "music/slidingdoor.ogg"
    scene SClass2
    show j 1 ch smile at fdis, five
    with fade
    "I take us down a couple flights of stairs to a different classroom that was having an event I found a bit fun."
    "It was also a restaurant like event (since those are kind of really easy to do and are all really popular with guests), but still different from our own class."
    j 1 ch think "\"Class 1-E? ... Why does that sound familiar to me?\""
    show j 1 ch watch at fdis, two with move
    show so 1 c smile at fdis, seven with moveiridis
    "Once we've taken a seat, a panda walks up to us, plopping a fancy laminated menu in front of us and smiling widely."
    show j 1 ch shock at fdis
    so "\"Welcome to \"Avenue 9\". I'm Sora and I'll be your server today. What can I help you with.\""
    "But Jun and my eyes go wide once we see him, suddenly surprised by the familiar face."
    j 1 ch shock "\"Ah, that's right! 1-E was Sora-kun's classroom!\""
    "He sputters out those words, ignoring the fact that there's no point in pointing out what has already been made obvious."
    "Sora pouts slightly, looking down at us with pitiable eyes."
    so "\"Aww, you mean you didn't come here just to see me? That's sad.\""
    j 1 ch shock "\"S-sorry, I didn't mea-\""
    play sound "music/tap.ogg"
    "The panda bellows out a laugh, giving Jun a tap on the back and smiling brightly at us."
    so "\"It's alright, I was just teasing you a bit, Senpai.\""
    show j 1 ch gentle at fdis
    "He sticks out a tongue and closes his left eye in a cheeky gesture, making Jun laugh beside me."
    show j 1 ch watch at fdis
    mc 1 c "\"I'm surprised to see you here. I came by yesterday and you weren't around. I didn't even know it was your class.\""
    "The panda's smile dims a little once he turns to look at me and he suddenly seems a bit more uncertain."
    so "\"Ah, w-well, I wasn't on shift yesterday so...\""
    j "\"The thing I'm most surprised about is how you're managing to work as a waiter, Sora-kun.\""
    so "\"What do you mean?\""
    "Sora peers down at Jun, cocking his head to the side, his emerald green eyes shining with curiosity."
    "The panda shifts around, putting one of his hands inside the pocket of his striped jacket."
    "I'm surprised he can walk around with those bulky headphones everywhere without the cable catching on anything."
    j 1 ch think "\"I mean... you're not exactly very good with people. You're really shy after all. And you're kinda clumsy.\""
    "Both of us, staring incredulously at the tiger."
    "Sora and I both look at each other, dumbstruck by the words we just heard."
    play sound "music/disappointment.ogg"
    "It's official. Jun doesn't have an ounce of self-awareness..."
    so "\"Heh... I guess you might be right. I decided to take this role since I wanted to practice my people skills. It's been really fun despite a few setbacks.\""
    "The bear laughs, his voice sounding very unrestrained and happy."
    "It's kinda funny to me just how much more comfortable he is when he's addressing Jun."
    "Every time he sees me or talks to me he tenses up something fierce, but as soon as he turns to look at Jun he noticeably relaxes."
    "I guess Jun kind of has that power on people."
    "He might be a clumsy, air headed, silly and sometimes slightly stupid guy, but..."
    "There is something about him that improves people's moods whenever he's around."
    so "\"Anyway, I can't spend too much time chatting. Why don't you guys take a look at the menu and decide what you want?\""
    "We nod, picking up the two menus he'd laid down on the table when we arrived."
    show Menu with dissolve
    "It's just as simple as I remember it being. Not many options, easy to remember but still looking kinda fancy."
    j 1 ch shockb "\"Woah. It's all desserts in here!\""
    so "\"Yes. Our café is focuses solely on cakes and cheesecakes. We don't have too many options but they're all homemade and good quality. We have two sisters in our class that are daughters of a patissier.\""
    mc 1 c talk "\"Oh, so that's why the stuff here is so good. They got their dad to prepare all the sweets?\""
    "Sora chuckles, shaking his head sideways in amusement."
    so "\"Nah. They made them from scratch themselves. They're really good bakers too.\""
    "I whistle, impressed at the skill these freshmen seem to have."
    "I guess this school really is filled with impressive people."
    so "\"Do you guys know what you'll be having?\""
    j 1 ch wince "\"Uhm... I dunno. There aren't many options but at the same time there are a lot. Besides, they're kinda-{nw}\""
    mc 1 c smile "\"Before you even think of saying they're expensive, just know I'm gonna be treating you.\""
    j 1 ch shockb "\"A-are you sure?\""
    mc 1 c considerate "\"Of course. It's only fair. I'm the one who invited you to come here.\""
    j 1 ch happyb "\"O-okay!\""
    "I watch Jun's tail swishing contentedly back and forth behind him. The sight makes me giggle."
    "For some reason, my chest feels very light right now."
    j 1 ch think "\"Hmm... I still don't know what to order...\""
    j 1 ch gentle "\"Ah, I know. [povFirstName]-san, order for me!\""
    mc 1 c shock "\"M-me? Are you sure?\""
    j 1 ch smile "\"Mhm. I'm confident you know what I like well enough, not to mention you've been here before. I think you'd make a better choice than I could. Faster too.\""
    mc 1 c considerate "\"Well... alright. Just don't complain if you don't like what I ordered.\""
    j 1 ch happy "\"Hehehe. I won't.\""
    "Hmm... somehow I doubt it."
    show j 1 ch smile at fdis
    hide Menu with dissolve
    "I pick up Jun's menu and put it together with mine, looking up to hand them to Sora-kun."
    "When I see his face again, I notice him looking between the two of us with a big, goofy smile on his face."
    mc 1 c talk "\"Is something the matter?\""
    so "\"N-no, nothing. I was just thinking over some stuff.\""
    "He tries to laugh it off but is visibly awkward."
    "Still, I decide to shrug and ignore it, making my order instead."
    "The last time I've been here I got some cake so this time I think I'll go with a cheesecake."
    "Besides, if I get something both Jun and I like for myself then I can share it with him later."
    "I'm sure he'd like that too."
    menu:
        "For me I'll get..."
        "Strawberry":
            $ junlove += 1
            "I'm personally fine with any of these three flavors of cheesecake so even someone as indecisive as me doesn't have much trouble choosing here."
            "But I think this one would be the one Jun enjoys the most and the one he'd be more likely to want a few bites of."
            "So it's probably the ideal option for this situation."
            mc 1 c talk "\"Okay, I'll have a strawberry cheesecake for me and...{nw}"
            $ cheesecake = "strawberry"
        "Lime":
            "I'm personally fine with any of these three flavors of cheesecake so even someone as indecisive as me doesn't have much trouble choosing here."
            mc 1 c talk "\"Okay, I'll have a lime cheesecake for me and...{nw}"
            $ cheesecake = "lime"
        "Mango":
            "I'm personally fine with any of these three flavors of cheesecake so even someone as indecisive as me doesn't have much trouble choosing here."
            mc 1 c talk "\"Okay, I'll have a mango cheesecake for me and...{nw}"
            $ cheesecake = "mango"
    menu:
        "For Jun I'll get..."
        "Blueberry Shortcake":
            "Hmm... blueberry is really good. I'm sure Jun would like it!"
            "He likes most things anyway so I don't see anything here that I think he'd dislike anyway."
            "Heh, this is a pretty safe list all things considered."
            mc 1 c smile "\"... and a blueberry shortcake for Jun.\""
            j 1 ch happy "\"Ah, that sounds pretty good.\""
            so "\"Gotcha. Anything to drink?\""
            $ cake = "blueberry"
        "Genoise Cake":
            "Hmm... the genoise here is pretty good. I'm sure Jun would like it!"
            "He likes most things anyway so I don't see anything here that I think he'd dislike anyway."
            "Heh, this is a pretty safe list all things considered."
            mc 1 c smile "\"... and a genoise cake for Jun.\""
            j 1 ch think "\"\"Je-nousu\"? I don't think I've ever had that before.\""
            mc 1 c smile "\"It's what I had the last time I was here. It's pretty light and delicate. I think you'll like it.\""
            j 1 ch happy "\"Oh, that does sound good!\""
            so "\"Gotcha. Anything to drink?\""
            $ cake = "genoise"
        "Castella Cake":
            "Hmm... castella is a pretty common cake so I'm sure he must have had it before. He'd probably like it."
            "He likes most things anyway so I don't see anything here that I think he'd dislike anyway."
            "Heh, this is a pretty safe list all things considered."
            mc 1 c smile "\"... and a castella cake for Jun.\""
            j 1 ch happy "\"Oh, my dad used to buy us castella when I was a kid. It's pretty tasty.\""
            "Nice. My gamble paid off."
            so "\"Gotcha. Anything to drink?\""
            $ cake = "castella"
        "Strawberry Shortake":
            $ junlove += 1
            "It's got to be strawberry."
            "It was a while ago but I remember that Jun likes strawberries."
            if cheesecake == "strawberry":
                "It's why I ordered the strawberry cheesecake after all."
            "I'm sure this is the ideal choice."
            mc 1 c smile "\"... and a strawberry shortcake for Jun.\""
            j 1 ch shockb "\"Ah...\""
            "Jun's face changes to one of astonishment, staring at me with an open mouth."
            mc 1 c happy "\"Let me guess. You're probably thinking \"Strawberry is one of my favorites. How did he know?\" aren't you?\""
            "Jun nods slowly."
            mc 1 c smile "\"Heh. It was kind of a while ago but do you remember when I bought you that stuff back at Ayako's shop?\""
            j "\"Yeah.\""
            mc 1 c happy "\"Back then the sweet you picked the most of was the strawberry shortcake.\""
            mc 1 c smile "\"You picked enough that even if both your parents got it, you'd still have a slice.\""
            mc 1 c happy "\"So I figured that probably meant you liked strawberry. Or at least strawberry shortcake.\""
            j 1 ch happyb "\"Waah, that's amazing, [povFirstName]-san. I couldn't remember all that stuff even if I tried.\""
            so "\"Gotcha. Anything to drink?\""
            $ cake = "strawberry"
    mc 1 c smile "\"What about it, Jun? Need me to choose it for you again?\""
    j 1 ch smile "\"Nah. I have no problem choosing what to drink.\""
    j 1 ch happy "\"Sora-kun, I'll have some orange juice.\""
    show j 1 ch smile at fdis
    if cake == "strawberry":
        mc 1 c smile "\"Oh? Not strawberry?\""
        j 1 ch considerate "\"I like it, but strawberry on strawberry feels a bit much, no?\""
        mc 1 c laugh "\"Hahaha. Fair enough.\""
    else:
        mc 1 c smile "\"Oh? Not soda?\""
        j 1 ch considerate "\"I'm already out of shape enough as it is. Don't need to pair a slab of cake with soda.\""
        mc 1 c smile "\"Heh. I didn't take you for the health conscious type.\""
        j 1 ch think "\"Uhm... I wouldn't go that far.\""
    show j 1 ch smile at fdis
    "Sora-kun writes down our order on a pad of paper he pulled out of his pocket, looking up from it to stare at me."
    so "\"What about you, [povLastName]-san?\""
    mc 1 c smile "\"I'll have some grape soda.\""
    "He finishes jotting it down and slips the pad back into his pocket, nodding at us."
    so "\"Alright. Your order will be out in a minute.\""
    show so 1 c smile at offscreenright
    show j 1 ch smile at fdis, five
    with move
    "The panda walks to the back of the room where a glass display case was neatly tucked away, showing off all the cakey goodness inside."
    "How they managed to get something so big and heavy inside the building I have no idea."
    "People were already throwing out their backs trying to get the burners and propane tanks to our class and those aren't nearly as heavy."
    "... But then again, they don't have to go up a bunch of flights of stairs to get here."
    "Yeah, never mind, they got it easy."
    show j 1 ch watch at fdis
    mc 1 c talk "\"By the way, Jun, how has the table tending been going?\""
    j 1 ch think "\"Pretty okay for the most part. I only had one rude customer but as soon as he started getting difficult to handle, Ayako-san showed up and glared at him until he left.\""
    "... So she's playing the part of bouncer now?"
    mc 1 c smile "\"Are you having fun doing it?\""
    j 1 ch wince "\"W-well...\""
    "Uh oh."
    mc 1 c worried "\"Something wrong?\""
    j 1 ch considerate "\"Not exactly {i}wrong{/i}. It's just that I really didn't think it'd be this stressful.\""
    j 1 ch wince "\"Having to deal with customers, memorize orders, get them to the right table, not spill any food, try to act cheerful the whole time.\""
    j "\"It's a lot.\""
    "..."
    "I don't want to be that guy but... I did tell you of all this before and you chose to ignore me and sign up for it anyway."
    "Why do you think so few people were signing up for it?"
    mc 1 c "\"What do you mean by \"memorize orders\"?\""
    j 1 ch think "\"Sometimes people can order a lot of stuff and it's hard to remember it all correctly.\""
    mc 1 c considerate "\"But why would you have to {i}remember{/i} it?\""
    j 1 ch watch "\"How else am I going to get the order to the kitchen?\""
    "..."
    mc 1 c sigh "\"... Do you not have a notepad? Did they forget to give you one?\""
    show j 1 ch shock at fdis, jumping
    j 1 ch shock "\"Ah!\""
    "Jun stares at me with wide open eyes, totally immovable for quite a few seconds."
    "Then, he slowly pulls something out of his pocket."
    j 1 ch considerate "\"I... totally forgot about it.\""
    "..."
    "....."
    "......."
    "{size=+4}How the hell do you forget about something that simple?!{/size}"
    mc 1 c considerate "\"Come on, Jun... you have to use your head more...\""
    j 1 ch wince "\"S-sorry...\""
    "I sigh."
    "I don't whether the fact that I already totally expect things like these from him should make me sad or not..."
    mc 1 c talk "\"You haven't had any accidents yet have you?\""
    j 1 ch wry "\"... Define accident.\""
    mc 1 c sigh "\"... Causing some kind of damage to a person or object.\""
    j 1 ch wince "\"D-define damage.\""
    "..."
    mc 1 c sigh3 "\"What the hell did you do?\""
    j 1 ch considerate "\"It wasn't that big a deal. I just... erm...\""
    mc 1 c sigh3 "\"What? Did you spill a bowl of soup on a guest?\""
    "I knew something like that was going to happen sooner or later anyway."
    j 1 ch avoid "\"{cps=5}...{/cps} I set a cloth on fire.\""
    play sound "music/tableknock.ogg"
    mc 1 c shock "\"{size=+4}You did what?!{/size}\""
    j 1 ch sigh "\"[povFirstName]-san, not so loud...\""
    "I can see a few heads turning towards us because of the sudden noise I made."
    "I clear my throat and readjust myself on my seat."
    mc 1 c sigh3 "\"I'm afraid of asking, but... How on Earth did you accomplish such a feat?\""
    j 1 ch wince "\"I-it wasn't my fault...\""
    "So... what? The cloth just upped and jumped onto an open flame on its own?"
    "How exactly would that even work?"
    mc 1 c sigh "\"Isn't the whole kitchen area walled off? How did you even get close enough to a burner to set something on fire?\""
    j 1 ch considerate "\"A guest sent some food back so I went around the wall to explain to Ginichirou-kun what the issue was.\""
    j 1 ch avoid "\"... The cloth I had tucked in the waist of my pants was flapping around a bit and it caught on one of the burners.\""
    j 1 ch wince "\"I was lucky I caught it before it torched my clothes.\""
    "..."
    "There are so many things wrong here I don't even know where to start."
    "First of all, why did he even have a cloth tucked in his pants?"
    "... You know what, I'm not opening that can of worms."
    "I just know no matter what question I ask, he'll find a way to surprise me."
    mc 1 c considerate "\"At least you didn't hurt yourself. That would have been bad.\""
    j 1 ch avoid "\"Yeah... That was why I was a bit late to meet you. I was apologizing to everyone and cleaning up the mess I made.\""
    mc 1 c shock "\"O-oh. I didn't know that.\""
    "... Now I feel kinda bad for grumbling over him being late."
    "... Even if he's the one who nearly set himself on fire."
    mc 1 c "\"Are you still going to go back for your next shift considering what happened?\""
    j 1 ch wince "\"Of course. I took up this job, I have to see it to completion.\""
    j 1 ch wry "\"... Even if Ayako-san was trying to convince me that I didn't have to.\""
    j 1 ch considerate "\"I get the feeling she might be trying to get rid of me.\""
    "Oh, ouch. If Jun managed to notice that then she really must have abandoned all attempts at subtlety."
    "Class Rep, I'm sorry you have to deal with this."
    show j 1 ch watch at fdis
    mc 1 c smile "\"If you dropped out of it we could spend more time going around the festival having fun.\""
    j 1 ch considerate "\"Yeah, but then a lot of the other students would have to have their times arranged. They would get their breaks shortened even more. They'd barely have time to enjoy themselves.\""
    mc 1 c worried "\"Hmm... I guess that's true.\""
    j 1 ch happy "\"I'm okay to continue, you don't need to worry about that. All things considered, I'm still having fun!\""
    "... You nearly turned yourself into a living torch and you have the guts to tell me you're having {i}fun{/i}?"
    "Either you're lying or there is something {i}reaaally{/i} wrong with you."
    "... And that's coming from me."
    j 1 ch considerate "\"[povFirstName]-san, you're staring at me with a really scary face right now.\""
    mc 1 c sigh3 "\"I'm just trying to decide how annoyed I should be right now.\""
    j 1 ch avoid "\"I-I'm sorry. I swear I'll be more careful.\""
    mc 1 c smile "\"Oho. You actually knew what I was annoyed over? I'm surprised.\""
    j 1 ch considerate "\"{size=-4}You know, sometimes you seem to have this idea that I'm really stupid for some reason...{/size}\""
    "I'm gonna pretend I never heard that."
    mc 1 c "\"By the way, didn't you find it weird that Sora-kun was wearing a hoodie instead of some kind of uniform?\""
    j 1 ch sigh "\"Now you're ignoring me?\""
    j 1 ch "\"Anyway, yeah, I noticed that. But if you look around, none of the waiters here are wearing uniforms.\""
    mc 1 c considerate "\"That doesn't make it any less weird. Why would you open a restaurant and not have uniforms?\""
    j 1 ch smile "\"I don't know but I kinda like it. It gives the place this casual vibe that is really comfortable.\""
    "They have busts, sculptures and velvet fabric all over the place in the decor. I'm pretty sure casual isn't what they were going for."
    "Which is exactly why it feels so weird."
    j 1 ch watch "\"You've been here before, right?\""
    mc 1 c smile "\"Sure have. Why do you ask?\""
    j 1 ch think "\"I was just thinking how good the cakes here must be. They're kinda pricey.\""
    mc 1 c considerate "\"They are, aren't they?{nw}"
    show mc 1 c smile at fdis, offscreenleft
    extend " But they're really good so I definitely think it's worth it.\""
    j 1 ch happy "\"I'm glad to hear it. I was a bit worried.\""
    show j 1 ch watch at fdis
    mc 1 c smile "\"By the way, I tried to pick something I thought you'd like for my cheesecake too so I can give you a few bites. That way you get to try two desserts.\""
    if cheesecake == "mango":
        j 1 ch considerate "\"I... I think I'll pass. Thank you though.\""
        show j 1 ch wry at fdis
        mc 1 c "\"Why? You don't even wanna try it?\""
        j "\"To be honest... I'm not a big fan of mango...\""
        mc 1 c worried "\"O-oh... I-I didn't know. I'm sorry.\""
        j 1 ch happy "\"You don't have to worry about it, [povFirstName]-san. I appreciate the thought anyway.\""
    elif cheesecake == "lime":
        j 1 ch smile "\"Nah. I appreciate the thought but I'll pass.\""
        mc 1 c "\"Aww, how come? Not even a bite?\""
        j 1 ch happy "\"I prefer not to eat other people's food. It just makes me feel weird. I'm sure it must be delicious though.\""
        mc 1 c smile "\"Alright, fine. I'll give you a pass this time.\""
        j "\"Hehehe.\""
    elif cheesecake == "strawberry":
        j 1 ch shockb "\"Wha- I...\""
        mc 1 c smile "\"What? Cat got your tongue?\""
        j 1 ch blush2 "\"N-no. It's just... I usually don't like to eat off of other people's plates but...\""
        mc 1 c happy "\"Buuuut?\""
        j "\"... I-I really like strawberry so I kinda... want to...\""
        mc 1 c laugh "\"Hahaha, I'm glad to hear it. It's why I ordered it so feel free.\""
        if cake == "strawberry":
            $ junlove += 1
            j "\"I can't believe you remembered I like strawberry...\""
            mc 1 c smile "\"Well, I can forgive you for not believing it. I do tend to have bad memory after all.\""
            j 1 ch watch "\"How did you then?\""
            mc 1 c think "\"Hmmm... I'm not sure why it was.\""
            mc 1 c happy "\"I just have a really easy time remembering things when they're about you. For some reason you always stick on my mind. Isn't that weird? Hahaha.\""
            show j 1 ch shockb at fdis
            $ renpy.pause (1.0)
            j 1 ch blush2 "\"Y-yeah. It's really weird.\""
    show j 1 ch watch at fdis, two
    show so 1 c smile at seven
    with move
    so "\"Alright, I'm back. Sorry for the delay.\""
    "Sora-kun sets the plates neatly in front of us. Even the plates and silverware here are very fancy."
    "Which, honestly, makes the dress code here feel really weird to me."
    j 1 ch shock "\"Wow. The slices are kinda big.\""
    mc 1 c smile "\"Yeah, they're pretty generous. Isn't it neat?\""
    j 1 ch happy "\"It is!\""
    so "\"I hope you guys enjoy it. Make sure to call me if you need anything else.\""
    j "\"Thank you, Sora-kun!\""
    "The panda smiles, nodding."
    so "\"You're welcome!\""
    show j 1 ch smile at fdis, five
    show so 1 c smile at offscreenright
    with move
    "He immediately walks off to go check on other tables."
    "There only seem to be three waiters for over thirty tables so I guess they must be pretty busy."
    "The place isn't so full that there are no open seats but it's still quite packed."
    "Good for them, they're getting a lot of attention."
    if cake == "strawberry":
        j 1 ch shockb "\"Oooh!\""
        show j 1 ch happyb at fdis
        "I turn to look at Jun and see a look that I can only describe as bliss."
        "He is carefully munching on a piece of his shortcake with a big smile on his face."
        j "\"Mmmmmmm~\""
        mc 1 c smile "\"You liking it?\""
        "He nods enthusiastically, not even saying a word."
        mc 1 c happy "\"How is it?\""
        j "\"It's so creamy~\""
        "His voice comes out a little higher than what I'm used to hearing and that immediately makes me laugh."
        mc 1 c laugh "\"Hahaha, I'm glad you like it.\""
        j 1 ch smile "\"[povFirstName]-san, you've got to try it!\""
        mc 1 c smile "\"Well... alright. I'm not gonna say no to that.\""
        show j 1 ch gentle at fdis
        "I reach for my fork so I can take a bite but before I have the chance to do so, Jun cuts a piece off with his own and puts his fork in front of my face."
        j "\"Here.\""
        "The gesture catches me off-guard for a second."
        "My eyes dart between him and the fork for a few seconds before I shrug and decide to just go with it, biting into the moist cake."
        mc 1 c happyb "\"Mm~\""
        "I already expected it to be good since I came here yesterday but the assault of flavors in my mouth is enough to take me by surprise."
        "The cake is moist, creamy and airy."
        "The strawberry brings just a tiny bit of tartness while the cream is deliciously sweet and light."
        "The cake base itself is surprisingly tender but also not overly sweet."
        "I was afraid the whole thing would be too sweet when I saw the amount of cream in it but it's so well balanced it could bring me to tears."
        j 1 ch happyb "\"Isn't it great?!\""
        mc 1 c happy "\"It really is.\""
        if cheesecake == "strawberry":
            mc 1 c smile "\"Do you want a bite of mine?\""
            j 1 ch considerate "\"Hehe... I'd be lying if I said I don't.\""
            show j 1 ch shockb at fdis
            "I haven't even tasted it myself but I still cut off a piece with my fork and extend it to Jun just like he did with me."
            j "\"Ooooh.\""
            show j 1 ch happyb at fdis
            "For some reason, the gesture seems to make him really happy."
            "While I'm still holding the fork, he gingerly leans forward on the table and takes a bite."
            j "\"Wow. This is so good too~\""
            "The tiger put his left hand on his cheek, chewing in absolute delight."
            "I try my best to stifle a laugh as the whole thing just feels so incredibly silly but fun at the same time."
            mc 1 c happy "\"I'm glad you like it.\""
            j "\"I love it. Thank you so much!\""
            "Hehehe. Pampering Jun is so much fun. I need to do it more often."
    else:
        j 1 ch happy "\"Ah, this is pretty good.\""
        "Jun takes a bite out of his cake, seeming very pleased with the flavor."
        j 1 ch smile "\"I'm surprised. At first I thought they were too expensive but they're definitely worth the price.\""
        mc 1 c happy "\"I'm of the same opinion.\""
        j 1 ch happy "\"Thank you, [povFirstName]-san. I guess you made a good choice after all, hehe.\""
        "Well... I don't think you'd have disliked any of the choices one way or another."
        "Still, I won't lie that it makes me happy to hear that."
        if cheesecake == "strawberry":
            mc 1 c smile "\"Do you want a bite of mine?\""
            j 1 ch considerate "\"Hehe... I'd be lying if I said I don't.\""
            show j 1 ch shockb at fdis
            "I haven't even tasted it myself but I still cut off a piece with my fork and extend it to Jun just like he did with me."
            j "\"Ooooh.\""
            show j 1 ch happyb at fdis
            "For some reason, the gesture seems to make him really happy."
            "While I'm still holding the fork, he gingerly leans forward on the table and takes a bite."
            j "\"Wow. This is so good too~\""
            "The tiger put his left hand on his cheek, chewing in absolute delight."
            "I try my best to stifle a laugh as the whole thing just feels so incredibly silly but fun at the same time."
            mc 1 c happy "\"I'm glad you like it.\""
            j "\"I love it. Thank you so much!\""
            "Hehehe. Pampering Jun is so much fun. I need to do it more often."
    stop music2 fadeout 2.5
    play music3 "music/BGM/Snowy Day.ogg" fadein 5.0
    play sound "music/slidingdoor.ogg"
    scene SCorridor
    show j 1 ch smile at fdis, five
    with fade
    so 1 c smile "\"Please come back later!\""
    "Sora-kun waves us off as we leave the café, an enthusiastic smile on his face the whole time."
    mc 1 c smile "\"For a guy who's supposed to be shy, he didn't do so badly as a waiter.\""
    j 1 ch happy "\"Well, I'm pretty sure the fact we're familiar faces helped.\""
    "You mean the face {i}you're{/i} a familiar face helped."
    "Jun and I both lost track of time while we chatted and ate our desserts."
    "Even something as silly as eating cake can be so much fun, it surprised me."
    j 1 ch smile "\"Thank you for treating me, [povFirstName]-san.{nw}"
    show j 1 ch happy at fdis
    extend " I had a lot of fun.\""
    mc 1 c smile "\"I'm glad you did. Is there anything else you want to do?\""
    j 1 ch think "\"Hmm... I don't really know any of the other stalls so I can't really say.\""
    mc 1 c happy "\"I guess that's fair. Let me try to think of something.\""
    j 1 ch watch "\"What time is it anyway?\""
    "Jun pulls his phone out of his pocket."
    "Unlike pretty much most of the people I know, Jun still uses an old flip phone."
    "Strapped to it are two keychains."
    if keychain == "janus":
        show rufus with dissolve
    else:
        show janus with dissolve
    "One of them is the one we bought yesterday. I guess he didn't waste any time putting it on."
    hide rufus
    hide janus
    with dissolve
    show keychain with dissolve
    "The other is the little tiger keychain I gave him when he was still in the hospital."
    mc 1 c talk "\"Oh hey, you decided to use these as phone straps instead of keychains?\""
    hide keychain with dissolve
    j 1 ch think "\"Huh?{nw}"
    show j 1 ch watch at fdis
    extend " Oh, you mean these?\""
    "He looks at the back of his phone to the two keychains that are now tied to his phone like straps."
    j 1 ch smile "\"I use my phone a lot more often than I do my keys and I wanted to always be able to see them and remember about them so I thought that would be a better way to go at it.\""
    mc 1 c smile "\"Guess you're the sentimental type, huh?\""
    j 1 ch considerate "\"I suppose... I had the little tiger on my keys for a while for I didn't really like having it there.\""
    j 1 ch smile "\"Mizoguchi-san is the one that gave me the idea of hanging them from my phone. I really liked it so I did it last night.\""
    mc 1 c happy "\"Well, so long as you're happy I'm happy.\""
    j 1 ch happy "\"Hehehe, I am. Especially with the little tiger. That one is my favorite.\""
    mc 1 c talk "\"Oh? I thought it'd be the one from the game. Didn't you say you loved the game.\""
    j 1 ch smile "\"Yeah, I do. But that one is just something I bought for myself.{nw}"
    show j 1 ch happyb at fdis
    extend " This one is a present you gave me. It's much more precious to me.\""
    play sound "music/heartbeat.ogg"
    "!"
    mc 1 c flustered "\"O-oh. I-I see. That's pretty nice of you to say, Jun.\""
    "He nods enthusiastically, flipping his phone open."
    j 1 ch watch "\"Hmm... We spent over an hour at the café. I have less than two hours until my shift starts again.\""
    mc 1 c worried "\"Aww. That little time?\""
    j 1 ch happy "\"It's not that bad. Besides, we still have the rest of the festival to spend together.\""
    mc 1 c considerate "\"I... guess that's true.{nw}"
    show mc 1 c smile at fdis, offscreenleft
    extend " What times will you have free tomorrow?\""
    j 1 ch considerate "\"Oh, uhm... I won't really have much of a break tomorrow.\""
    mc 1 c shock "\"What?! You're kidding!\""
    j 1 ch think "\"We're really strapped for help since most of the class bailed on serving food.\""
    j 1 ch considerate "\"Every single one of the waiters is having a day where they have no breaks. Mine is tomorrow.\""
    mc 1 c sigh "\"That's awful! I'm gonna walk up to Class Rep and Kyoko and give them both a piece of my mind.\""
    j 1 ch annoyed "\"Sure. The guy who bailed and is one of the reasons I have to pull that kind of shift is going to complain that I have to pull that kind of shift. I can already see that going so well.\""
    play sound "music/stab.ogg"
    "Guh..." with hpunch
    "Sometimes he can have a surprisingly sharp tongue."
    mc 1 c considerate "\"... Sorry.\""
    j 1 ch considerate "\"You don't need to apologize. I'm not complaining. I'm the one who wanted to do this.\""
    j 1 ch avoid "\"I just don't want you giving them grief over something they have no power to change. Besides, it'd be a bit hypocritical.\""
    j 1 ch wry "\"Well... A lot hypocritical.\""
    mc 1 c sigh3 "\"Right... I didn't think of it from that scenario.\""
    j 1 ch think "\"Well, you do have a tendency not to.\""
    mc 1 c sigh "\"Hey!\""
    j 1 ch happy "\"Hehehe. What? I'm just saying.\""
    mc 1 c sigh2 "\"You're lucky you're cute otherwise I'd give you a piece of my mind too.\""
    show j 1 ch shockb at fdis
    $ renpy.pause (1.0)
    j 1 ch blush2 "\"I-I'm not...\""
    mc 1 c smile "\"Sorry, that flimsy little retort isn't going to change my mind.\""
    j "\"Nuu...\""
    mc 1 c "\"Hmm... I wonder where we could go right now. I know there's a second year class that is putting on a play. Maybe we could watch that.\""
    j 1 ch shock "\"Oh, that sounds like a lot of fun!\""
    mc 1 c wince "\"But they only do it three times a day and I think the afternoon one is already underway. The next one would only be during the night.\""
    j 1 ch wince "\"Aww...\""
    mc 1 c smile "\"How about we go watch it later? You're gonna have a break during the last day of the festival, right?\""
    j 1 ch "\"Yeah. I'll get off right after lunch time and won't have to go in anymore.\""
    mc 1 c smile "\"Yeah. Every single stall is gonna be closed for the bonfire and the fireworks show.\""
    j 1 ch considerate "\"I can't wait for the fireworks!\""
    mc 1 c smile "\"We have to see if we can get everyone together for it.\""
    j 1 ch smile "\"I wanna go watch it from the roof!\""
    "His eyes are nearly sparkling as he says that, his tail is lashing wildly behind him."
    "I guess the idea must have him really excited."
    mc 1 c "\"Why the roof?\""
    j 1 ch happy "\"I want to watch from the highest place possible so I'll have the best view.\""
    mc 1 c smile "\"Oh, is that why? Bad news for you though, the roof is kept locked during the festival to avoid any students slipping off to go there.\""
    j 1 ch shock "\"Whaaaaat?!\""
    mc 1 c considerate "\"The school had lots of issues with students going upstairs in droves with plates of food and whatnot and just leaving them scattered around so they started locking it.\""
    mc 1 c "\"This was before I even started going here. Must have been a while ago.\""
    j 1 ch avoid "\"Aww... I really wanted to see it from the roof...\""
    mc 1 c think "\"Hmm... Maybe Shoichi could get me a key? I don't know, I'll see what I can do.\""
    j 1 ch shockb "\"R-really?!\""
    mc 1 c wince "\"I just said that I'm gonna see what I can do. I don't know that I'll be able to.\""
    j 1 ch gentle "\"Yay. I'm counting on you, [povFirstName]-san!\""
    "You didn't hear a single word I just said did you?"
    "..."
    "Well, worst comes to worst, I'll try to learn how to pick a lock with a paper clip I guess."
    "How hard can it be? I'll just practice on an old padlock until I get."
    "Hopefully I won't need to do it though."
    mc 1 c smile "\"Anyway, how about we go walk by the courtyard and see what other displays they have?\""
    j 1 ch happy "\"Yeah!\""
    "He nods emphatically, almost jumping on the spot."
    "His excess energy whenever he's happy or excited really gives me life."
    "Seeing him like this makes my day."
    mc 1 c think "\"Let's see... I seem to recall a few students doing an open air flower garden. Might not be much but you might like seeing and smelling the flowers.\""
    j 1 ch smile "\"Oooh, that sounds great.\""
    "Almost everything sounds great to you though."
    stop music3 fadeout 2.5
    stop music fadeout 2.5
    scene BedroomN with fade
    show hours with dissolve
    $ renpy.pause (1.0)
    hide hours with dissolve
    "As much as I love the school festival, the constant noise and always being surrounded by so many people gives me a headache."
    "As soon as I arrive home, I flop down onto my bed and just let myself be lazy there for a few minutes."
    "I hope Jun is doing okay. It seemed like the festival got more and more crowded as the hours went by."
    play sound "music/phonebeep.ogg"
    "Just to make sure, I'll shoot him a message."
    "\"Hey. How was your second shift? Hopefully no more accidents hahaha. Just checking up to see how you're doing. Message me when you can.\""
    play sound "music/message.ogg"
    "Oh, he answered right away!"
    "Jun: \"I'm helping them clean up here before going home. It was pretty okay. I had another rude customer today which kinda sucked though.\""
    "\"Oh no! Was he really nasty to you?\""
    play sound "music/message.ogg"
    "Jun: \"Kinda? But then Ayako-san yelled him out of the room. After that word kinda spread and everyone was in their best behavior lol\""
    "..."
    "I can totally picture Class Rep doing something like that."
    play sound "music/disappointment.ogg"
    "That's a very scary thought actually..."
    "\"I'm glad things went better. Just take care of yourself going home. I got back a few minutes ago and have just been lazing in bed.\""
    play sound "music/message.ogg"
    "Jun: \"I wouldn't mind having a soft bed to lie on. [povFirstName]-san, your house is closer than mine, let me use yours!\""
    "Pfft."
    "I snicker reading that one message."
    "I just pick Jun acting like a house cat and kicking me out of my bed so he can have it all for himself."
    "\"I don't really feel like sleeping on the floor so I'll have to pass.\""
    play sound "music/message.ogg"
    "Jun: \"Aww, we could have just shared...\""
    play sound "music/heartbeat.ogg"
    "?!"
    "My eyes shoot open as soon as I read the message."
    "I look at it for a few seconds not knowing how to respond. As soon as I prepare myself to respond, I receive another one."
    play sound "music/message.ogg"
    "Jun: \"Oh, Kyoko-san is giving me the evil eye because I'm texting lol. I'll talk to you later!\""
    "..."
    "I decide to just toss my phone onto the bed and roll to the other side."
    "Before I notice it, sleep begins to wash over like countless waves."
    "Yawning, I decide to take off my shirt and just go to sleep."
    $ date = None
    jump Day19_Jun
