label Day17_Keisuke:
    window hide
    scene May26 with fade
    play sound "music/windchimes.ogg"
    show blossoms movie
    $ renpy.pause(5.5)
    play music2 "music/BGM/City Pulse.ogg" fadein 5.0
    scene Station with fade
    window show
    $ date = "day17"
    "Aaah, the weather today truly is wonderful."
    "In stark contrast to that ridiculous typhoon that came out of nowhere a couple days ago, today things are sunny and cool."
    "The sky is blue with just a few clouds lazily passing by."
    "Thankfully, the streets aren't jampacked with people at this time."
    "I came here right after class at someone else's insistence."
    show k 1 u smile at fdis, three
    show al 1 u at fdis, seven
    with dissolve
    "That someone was of course, Keisuke."
    "Even Alex has joined us again today too."
    "It's funny, I'd known Kei-kun for over a year and never met the guy but now I feel like I'm seeing him every week."
    mc 1 u "\"Not that I'm complaining but... would you mind telling me what you called me over for now?\""
    if day10 == "keisuke":
        k "\"Sure. I was called over by Kiyoshi-san. He said he wanted to talk to me about something.\""
        mc 1 u think "\"Kiyoshi?\""
        "How come that name sounds so familiar."
        al "\"He's the owner of the music shop Keisuke frequents. Or at least used to frequent.\""
        k 1 u "\"Yeah. If you discount the time you went there with me, the last time I'd been to that place was a few years ago.\""
        mc 1 u shock "\"Oh, you mean the lizard guy with the really weird sense of humor?\""
        show k 1 u smile at fdis
        al "\"That sounds like a very apt description of him.\""
        k "\"Indeed it does.\""
    else:
        k "\"I was called over by an acquaintance of mine. He said he wanted to talk to me about something in the shop he owns.\""
        mc 1 u "\"You're friends with a shop owner? That's pretty impressive.\""
        k 1 u "\"Actually, I think I should refer to him as my father's friend instead. That one is more factually correct.\""
        mc 1 u smile "\"Still impressive.\""
        al "\"The man used to be Keisuke's guitar teacher for a few years.\""
        mc 1 u laugh "\"Oh, so he probably owns a music store then!\""
        k 1 u "\"No. He's a guitar teacher that owns a gun shop.\""
        mc 1 u shock "\"Wait, seriously?!\""
        k 1 u annoyed "\"No, you git. I was being sarcastic. Of course he owns a music store.\""
        mc 1 u wince "\"Oh, ow. You didn't need to be so mean.\""
        al "\"You were basically asking for it.\""
        show k 1 u smile at fdis
        mc 1 u sigh "\"Hey!\""
        "The two choose to ignore my grumbling, leaving me following behind them unhappily."
    al "\"Do you have any idea what it is that he wants, Keisuke?\""
    k 1 u "\"Nope. Not a clue. I'm hoping he just wants to catch up. He did say he wanted us to keep in touch the last time I visited.\""
    al "\"Did he by any chance do anything... untoward with you?\""
    k 1 u smile "\"You know he always does, Alex. But that doesn't mean I want you to murder him.\""
    al "\"Not even a little?\""
    k 1 u calm "\"Hmm... maybe later.\""
    al "\"Understood.\""
    "... I don't get if that was supposed to be a joke."
    "Someone please say \"haha, just kidding\" or something like that."
    mc 1 u "\"Okay, fine, I understand why {i}you're{/i} coming here but I still don't see why I was called.\""
    k 1 u "\"I... dunno. I just felt like I wanted to go out with you. Is that so bad?\""
    mc 1 u worried "\"I didn't say that.\""
    k 1 u eyesc "\"If you don't want to be here then you can go home.\""
    "I didn't say that either!"
    mc 1 u considerate "\"Well... it's not every day that I have the chance to check out a music store so I'll stick around.\""
    k 1 u smile "\"Good boy!\""
    "... Did he just say \"good boy\"?"
    "... Unironically?"
    "The fuck?"
    scene MusicStore
    show k 1 u smile at fdis, five
    show al 1 u at nine
    show ki 1 u smile at two
    with fade
    "Just as soon as we walk into the store, we see a green lizard man standing behind the counter and talking to a few customers."
    "Kei-kun makes a beeline towards him and the lizard notices our approach halfway through it."
    "He opens a wide smile and waves at us as soon as he finishes dealing with his customer."
    ki "\"Well well, if it isn't Kei-chan and little Alexander. Thank you for coming by on such short notice!\""
    "Did I just get totally ignored?"
    k 1 u sigh "\"I can't believe the first words I say as soon as I meet you are gonna be these, but... Don't call me Kei-chan.\""
    ki "\"It's good to see you too, dear.\""
    k 1 u avoid "\"Eugh, don't call me that either.\""
    "The lizard turns to look at the towering wolf, having to look up to meet Alexander's eyes."
    ki "\"Good to see you too, Alexander. Still looking as sharp as ever, I see.\""
    al "\"Hello, Kiyoshi. I wish I could say the same but your shirt is crinkled and popping out of your pants.\""
    "Kiyoshi looks down, noticing the mess that is his clothing."
    "Smiling, he readjusts them."
    ki "\"Oops, sorry about that. Two of my staff called in sick today so I was loading stock into the shelves myself. Got a little sloppy.\""
    al "\"That is a shame. The one good thing about you is how neat you are.\""
    ki "\"What do you mean \"one good thing\"? I have tons of good things!\""
    k "\"Last time I was here he got polish all over my shirt.\""
    al "\"I said he was neat, not that he was clean.\""
    ki "\"Hey, I {i}am{/i} clean! ... Sometimes.\""
    al "\"Mhm.\""
    "Well, it's clear that these three are already plenty familiar with each other just from their way of speaking."
    if day10 == "keisuke":
        "But then again, I already knew that from the last time I was here."
        "... And also like last time, I'm totally getting sidelined and left out of the conversation here."
        "Can't say I care too much for that."
    else:
        "... Although I can't shake off the nagging feeling that I'm being forgotten about here."
        "The guy didn't even acknowledge me when we showed up."
    k 1 u "\"So, Kiyoshi-san, what was it that you called me over for?\""
    ki "\"Oh, it wasn't anything too important. Why don't we catch up first? What have y-\""
    k 1 u sigh "\"Kiyoshi-san...\""
    "Kei-kun speaks sternly, cutting the lizard off mid sentence."
    "For his part, Kiyoshi pouts at the hare's attitude."
    ki "\"Jeez, not an ounce of love for good ol' uncle Kiyoshi here. I see how it is.\""
    k "\"Would you stop with the dramatics? The three of us know that if we played along with your antics we'd be stuck here for the rest of the day.\""
    "Three of us? Is even Kei-kun forgetting to count me here?"
    "Someone please notice that I exist!"
    ki "\"Oh ouch, you wound me Kei-chan-boy. Although let's be honest, would that really be so bad?\""
    k 1 u "\"Yes it would. You wouldn't be able to tend to your shop and I wouldn't be able to do all the other things I need to do with my day.\""
    al "\"Yes, Keisuke is quite busy as you know.\""
    "Kiyoshi-san crosses his arm, looking between the two with a raised eyebrow and a smug smile."
    ki "\"Ah yes, indeed. So busy that he was able to show up here after I called him up totally out of the blue.\""
    "Kei-kun and Alex look at each other."
    k 1 u worried "\"I swear, people keep calling me out on that and I never have an answer to it.\""
    show k 1 u shock at fdis, jumping
    mc 1 u "\"That's because you keep pretending to be super busy when you're really not.\""
    k "\"Wah! [povFirstName]-san!\""
    ki "\"Why hello there, I was wondering when you were going to speak up.\""
    mc 1 u sigh "\"You totally forgot I was here didn't you, Kei-kun?\""
    k 1 u worried "\"N-no. I was just... distracted.\""
    mc 1 u sigh "\"Sure.\""
    if day10 == "keisuke":
        ki "\"I was actually beginning to wonder if your friend had gone mute since the last time he'd been here since he didn't even bother to respond to my greeting.\""
    else:
        ki "\"I was actually wondering if this friend of yours was a mute since he didn't even respond to my greeting.\""
    mc 1 u sigh "\"Your greeting didn't even include me. I was totally ignored.\""
    ki "\"Details, details. You shouldn't sweat the small stuff so much!\""
    "This guy really is the annoying type isn't he?"
    k 1 u sigh "\"[povFirstName]-san, it goes without saying but don't pay him much attention. Kiyoshi-san loves getting under people's skin.\""
    mc 1 u "\"I noticed.\""
    ki "\"Whaaaat? I would never! I am nothing more than a gifted conversationalist who enjoys chatting with my good fellows!\""
    al "\"Kiyoshi...\""
    ki "\"Yes, my little Alexander dear?\""
    al "\"You're annoying.\""
    play sound "music/stab.ogg"
    show ki 1 u smile at shake1
    "W-wow..."
    "I never thought I'd see Alex of all people calling someone annoying so bluntly."
    "If that guy can get under even Alex's skin then he really has a talent for it after all."
    "Kiyoshi-san pouts, suddenly looking very sour."
    ki "\"Jeez, you come all the way to a guy's humble little shop just to hurt his feelings. And hurt them you do with mastery. So mean...\""
    k 1 u sigh "\"Kiyoshi-san, could you please act your own age? You're not a child anymore.\""
    ki "\"How rude!\""
    "While I have to admit that this back and forth is pretty amusing, constantly switching between these three is starting to make my neck hurt."
    mc 1 u considerate "\"Uhm... can we get back on track here. You called Keisuke over for something?\""
    k 1 u "\"Yeah, right. Thanks for reminding me, [povFirstName]-san.\""
    ki "\"Aww, and here I thought I was doing a good job of distracting everyone. Quick, look, a three-headed monkey!\""
    k 1 u doom "\"Start telling me why you called us over right now or I'm walking out.\""
    ki "\"Sheesh, no sense of humor...\""
    "I think you have way too much \"humor\" for your own good."
    "The lizard sighs, closing his eye and rubbing the bridge of his nose for a few seconds."
    "I think this is the first time I ever saw him looking anything other than peppy and cheerful."
    ki "\"Well, it's just that... I heard you'd joined a band and that you guys were even going to be playing on your school's festival. I wanted to congratulate you on that.\""
    show k 1 u serious at fdis
    "Kei-kun's face immediately loses any and all traces of humor."
    "The hare stares at the older man with a sharp, unfriendly look on his face."
    "It's enough to make me get chills."
    k "\"Who told you that?\""
    "Kiyoshi-san smiles, completely ignoring Keisuke's death glare."
    ki "\"Oh, you know exactly who told me. It was your good ol' pop. Who else would it be? {size=-2}Also, do you really have to look at me like that? It's a bit disturbing.{/size}\""
    play sound "music/tap.ogg"
    show k 1 u worried at fdis
    "Alex puts a hand on Kei-kun's shoulder, giving him what looked to be a gentle squeeze."
    al "\"Calm yourself, Keisuke. You don't even know what happened yet, there's no point in getting angry.\""
    ki "\"Yeah, that's right, Kei-chan-boy. You really shouldn't get angry so easily. If you frown so much your skin's gonna get all wrinkly.\""
    k 1 u sigh "\"Kiyoshi-san, can you please stop horsing around?\""
    "The lizard's eyes open wide and he looks down at himself in a panic."
    ki "\"Horse?! Oh my God, I thought I was a lizard! Did something happen to me?!\""
    k 1 u angry "\"Kiyoshi-san!\""
    play sound "music/tap.ogg"
    "Kiyoshi breaks his little act, chuckling to himself and giving Kei-kun multiple pats on the back."
    ki "\"Come on, you need to learn to relax, kiddo. You're too tense! Has something been stressing you out lately?\""
    "I swear, it's like watching a child cluelessly poking a hornet's nest."
    "... Deliberately... with a stick... on fire."
    k 1 u sigh "\"Can you just tell me what the hell did my father tell you?\""
    ki "\"Hmm... Well, there was a lot. Oh, he told me that he's been seing this girl lately and that she has a {i}really{/i} nice bo-{nw}\""
    k 1 u angry "\"{size=+4}About me!{/size}\""
    "God, watching these two talk is both hilarious and frustrating."
    "Even Alex is covering his face with his hand, slowly shaking his head sideways."
    "I guess their ridiculous conversation is too much even for the ever serious wolf."
    ki "\"Ooooh, about {i}you{/i}? Gosh, why didn't you just say so from the start? You can't blame a fella for getting confused if you're always this ambiguous.\""
    k 1 u doom "\"Kiyoshi-san... I'm five seconds away from telling Alex to hit you.\""
    "For the first time, the look on the lizard's face falters. He looks up at the wolf with bulging eyes and a wide open mouth, blinking much more rapidly than normal."
    "Alex, for his part, does nothing more than cross his arms and raise an eyebrow. For him, that's already enough to look menacing."
    ki "\"Okay okay, I'll tell you. You don't need to sic the dogs on me!\""
    k 1 u annoyed "\"Then stop being so annoying.\""
    ki "\"{size=-4}Sheesh, not an ounce of humor in this boy...{/size}\""
    "The lizard grumbles uselessly to himself, crossing his arms and staring down at his feet."
    ki "\"Well, let's see... your good ol' dad told me recently that you'd done gone and joined a school band. He even told me that you fellers were going to perform in your school festival. Major kudos for that by the way.\""
    ki "\"Of course, you know how your dad is. He asked me to give you my \"expert's opinion\"... which is just a nice way to say that I was supposed to put you down and convince you to quit.\""
    k 1 u avoid "\"And are you?\""
    "The lizard makes an open gesture with his hands, almost as if he were about to hug Kei-kun then and there."
    "His facial expression shifts once again, all the seriousness from before vanishes without a trace, leaving him with a beaming smile."
    ki "\"Cripes no! I told him to shove it and let you do whatever you want. In fact, I'm really proud of ya, kid. I've been hoping you'd do something like this for the longest time!\""
    k 1 u shock "\"Wha-\""
    "Kei-kun seems incapable of comprehending the words that were just spoken, standing rooted to his spot in a daze, mouth hanging open and eyes bulging."
    "The first one to break the silence is Alex, who smiles just the tiniest bit before speaking."
    al "\"Heh. Guess you're not so bad after all, Kiyoshi.\""
    "The lizard furrows an eyebrow, confused."
    ki "\"Not so bad? Since when have I been any kind of bad? I thought I was great!\""
    al "\"Don't push it.\""
    ki "\"Sorry.\""
    "I... have no idea what is going on anymore."
    k 1 u worried "\"So... you're not gonna try and get me to quit?\""
    ki "\"Nah! Don't be ridiculous. I'm actually gonna show up for your festival. I wanna see what this band that managed to enrapture you is like. It's not every day I get to see my Kei-chan-boy coming out of his shell.\""
    mc 1 u considerate "\"Erm... I feel like I need to point this out, but... Kei-kun isn't playing.\""
    show k 1 u wince at fdis
    "The smile on Kiyoshi's face suddenly vanishes."
    ki "\"What do you mean he ain't playing?\""
    k "\"W-well... I'm just... a manager.\""
    stop music2 fadeout 2.5
    ki "\"...\""
    k 1 u worried "\"...\""
    al "\"...\""
    mc 1 u considerate "\"...\""
    show k 1 u wince at jumping, fdis
    play music3 "music/BGM/Punchline - Org.ogg"
    ki "\"{size=+2}Whaaaaaaaaaaaaaaat?!{/size}\"" with hpunch
    k "\"Waaah! K...Kiyoshi-san, you're too loud!\""
    ki "\"What do you mean you're just a manager! You telling me I stuck up for you when you're just a glorified instrument polisher?!\""
    k 1 u worried "\"T-that's not all I do! I help them with tuning, I oversee their rehearsals-\""
    ki "\"I don't give a monkey's ass. Why the hell did you join a band to be a manager?!\""
    mc 1 u "\"I asked him pretty much the same thing too.\""
    "Although with nicer words."
    k "\"Come on, you guys are being too unreasonable. There's nothing wrong with being a manager...\""
    ki "\"There's everything wrong with being a manager! What was even the point with that? You're a phenomenal singer and a really good guitarist. Why are you settling for a managerial role?!\""
    k "\"Not everyone has the courage to go out and perform...\""
    mc 1 u considerate "\"They asked him to stand in for their vocalist during rehearsal one day and he choked.\""
    k 1 u shock "\"Wha- I didn't choke. I managed to sing!{nw}"
    show k 1 u annoyed at fdis
    extend " Also, shut up.\""
    stop music3 fadeout 2.5
    play music2 "music/BGM/Dazzling Sunlight.ogg" fadein 5.0
    "The lizard sighs, awkwardly scratching the back of his neck."
    ki "\"Damn, this is such a waste of good talent...\""
    k 1 u avoid "\"Be quiet. I enjoy being a manager and you can't make me feel bad about it.\""
    ki "\"What am I going to do with you?\""
    al "\"Kiyoshi, that's enough already.\""
    "Alex cuts into the conversation, sternly telling the older lizard to back off."
    ki "\"Yeah yeah, whatever. And here I was all excited about this too.\""
    k 1 u sigh "\"Why are you the one getting excited about it? It has nothing to do with you.\""
    ki "\"Hey, when something cool happens to my Kei-chan-boy, I have a right to get excited.\""
    k "\"Will you please stop calling me that already?\""
    ki "\"Nope!\""
    k "\"I hate you...\""
    ki "\"No you don't!\""
    "I'm surprised Keisuke hasn't already strangled this guy by now."
    k 1 u "\"Alright, great. You've said your piece. Can I go now?\""
    ki "\"Aww, is that all I am to you? Just say a few words and then leave? I thought we had something special!\""
    k 1 u sigh "\"...\""
    "At this point, I don't think Kei-kun has any fight left in him."
    "He looks so... defeated."
    k "\"Was there anything else you wanted to discuss or can I leave?\""
    ki "\"Well... I {i}was{/i} going to give you a present for joining your first band but now that I know you're just a manager I don't know if I should.\""
    k 1 u "\"A present?\""
    "His ears perk up as soon as he hears the word \"present\" being said."
    k "\"Knowing you it would probably be a gag gift wouldn't it?\""
    "Kiyoshi-san chuckles."
    ki "\"While that definitely sounds like something I would do, that wasn't the idea.\""
    al "\"It's not something that would explode or pop out of a box, is it?\""
    ki "\"I just said it's not a gag gift!\""
    "I don't believe that for even a second."
    ki "\"Here, hang on a second.\""
    "Kiyoshi-san dives behind the counter, disappearing out of our view."
    "Kei-kun leans against the wood and glass counter, peering trying to see behind it."
    "When he resurfaces, he's holding an old, dusty looking red acoustic guitar."
    ki "\"Here ya go!\""
    show k 1 u worried at fdis
    "He hands it to Kei-kun who looks down at it hesitantly."
    k "\"Your present to me is your... trash?\""
    "The lizard huffs and pouts, placing the guitar delicately on top of the counter and wiping some of the dust off of it with his hand."
    ki "\"Hey, I'll admit it's seen better days but this ain't trash. It's not super pricey or anything but this here was the first guitar I ever owned.\""
    k 1 u shock "\"What?!\""
    "Kiyoshi-san nods, proudly puffing his chest."
    ki "\"Yeppers. This here is the first guitar I ever bought when I decided I wanted to learn music. This is from way before I ever met your father. My dad was always very strict and said if we wanted to buy things, we needed to work for it ourselves.\""
    ki "\"I was originally gonna buy a really basic model but when I first laid eyes on this beauty, it was love at first sight. It took me over six months of part-time work to buy it but I have it to this day.\""
    ki "\"I realized that even if I still do love it, I don't have the time or energy to give it the care it deserves. So I thought it'd be best to pass it onto someone I know would take good care of it.\""
    k 1 u worried "\"Wow... Kiyoshi-san, I... I don't know. I don't think I'd be able to do that.\""
    ki "\"Nonsense. I've seen how you treat your guitar. That thing is always polished to a shine and always handled with care. Not even I ever treated my instruments that well. I'm sure this baby here will be in great hands.\""
    k "\"But... but... isn't it important to you?\""
    ki "\"It sure is. One of my most prized possessions. It's exactly why I can't stand to see it withering away in a display closet anymore. It's time I passed it on to someone else.\""
    "He turns his head away and I'm pretty sure I even catch a glimpse of a blush as the lizard rubs the back of his neck."
    ki "\"And... well... call me sentimental but I'm pretty sure the guitar itself would love to be with someone that would actually play it. I know acoustic ain't really your style but I just thought...\""
    k 1 u wry "\"I'll take good care of it, Kiyoshi-san.\""
    "Kiyoshi-san's face immediately lights up as he lifts the guitar and hands it to Kei-kun."
    ki "\"I know you will, kid. And thanks. It's not easy for me to let this baby go but I know it's for the best.\""
    k 2 u gentleb "\"Thank you. I... I really appreciate it.\""
    show k 1 u smile at fdis
    ki "\"Just let me grab a case for it from the stock. The one I had with it is way too damaged for me to give it to you in good conscience.\""
    k "\"Sure. Just as long as you let me pay for it.\""
    ki "\"Don't be ridiculous, it's a gift.\""
    k 1 u eyesc "\"Knowing you, you'd probably pick out the most expensive case you have in the store. {i}I'm{/i} the one who can't accept that in good conscience.\""
    ki "\"Heh. Guess you know me a little bit better than I thought.\""
    show ki 1 u smile at four
    show k 1 u at fdis, six
    show al 1 u at ten
    with move
    show zin 1 c at one with moveiledis
    "Just as Kiyoshi-san was about to step out of the counter, someone walks up from the back of the store."
    "A lean young man with silky white and gray fur and a somewhat ratty looking shirt approaches the lizard."
    "He even has... red fur on his head? Styled like bangs too."
    "I've seen some people do this in magazines or TV Shows but never ran into someone sporting one of those in real life."
    ukn "\"Kiyoshi-san, I'm going to have to leave fifteen minutes early today. Thanks again for getting me the room.\""
    "The lizard smiles, grabbing a pen and a notebook from behind the counter and plopping them over on top of the table."
    ki "\"It's no problem, kid. Thanks again for your patronage. Can I already book you for your next visit?\""
    ukn "\"Yeah, sure. Do you have any open slots three days from now?\""
    play sound "music/pageflip.ogg"
    "Kiyoshi-san looks down on his ledger, flipping a few pages while scanning all the lines."
    "Is that his list of bookings? That thing is absolutely filled to the brim!"
    "There's barely any room left blank on the pages."
    ki "\"Oh, here we go. I have two openings. One for 2PM and one for 7PM.\""
    "The young man pauses for a few second, crossing his arms and scratching his chin."
    "He makes an exaggerated \"hmmm\" sound as he thinks it over."
    ukn "\"I think 7PM is the best time for me. I'd probably be nn a meeting for the other time.\""
    ki "\"Alright, I'll jot it down here. See you Monday, kid.\""
    ukn "\"See ya!\""
    show placem at offscreenright with moveoridis
    show ki 1 u smile at two
    show k 1 u at fdis, five
    show al 1 u at nine
    with move
    "Kiyoshi-san waits for a few seconds, watching the boy leave before ducking out of the counter."
    ki "\"Ya know, I could introduce you two if you'd like.\""
    k "\"Introduce? Who? Me and that guy?\""
    ki "\"Yeppers. He's a really nice boy. Plays the bass. He's really good at it too. He recently joined a band but he's been anxious that it might not work out. Who knows, you could poach him for yourself.\""
    k 1 u sigh "\"The band already has a bassist. Besides, that would highly disrespectful to the other band.\""
    "The lizard shrugs, starting to walk away from us and towards the shelves."
    ki "\"Well, suit yourself. Who knows, if you ever get over that stage fright of yours, the two of you could one day perform on the same stage together.\""
    show ki 1 u smile at offscreenleft
    show k 1 u at fdis, three
    show al 1 u at seven
    with move
    "Kiyoshi-san disappears behind a few shelves, happily whistling a cheerful tune."
    al "\"Well... as far as interactions with Kiyoshi go, this one was one of the better ones.\""
    k 1 u smile "\"Yeah it was.\""
    "From the corner of my eye, I see Kei-kun gently cradling and petting his new guitar, looking down at it with affection."
    "The sight brings a smile to my face."
    scene Station
    show k 1 u calm at fdis, three
    show al 1 u at seven
    with fade
    "Despite his earlier insistence on leaving, Kei-kun easily spends another hour on the store chatting with Kiyoshi-san."
    "For most of the time, I don't get many chances to speak, but still I somehow end up enjoying myself."
    "Once those two finally run out of things to say, Kei-kun excuses himself and we leave."
    k "\"Hmm hm hmm hmmm hm~\""
    "Kei-kun merrily hums a melody as we walk, proudly carrying his new instrument around his shoulder in its fancy carrying case."
    "For some reason, I get the feeling that that case is worth more than the guitar itself."
    al "\"Keisuke, is there anything else you wish to do?\""
    k 1 u smile "\"Hmm? No, not really. That was about it.\""
    mc 1 u considerate "\"Glad it wasn't as painful as you thought it'd be, huh?\""
    k 1 u worried "\"Oh, err... right. Sorry for dragging you out here just to let you stand in a corner, [povFirstName]-san.\""
    mc 1 u smile "\"It's fine. I mean, look at me. Don't I make for a very handsome adornment?\""
    k 2 u gentle "\"For sure. A very handsome one indeed.\""
    "Somehow I didn't expect him to agree with me on that."
    "Especially that enthusiastically."
    "I... kinda feel embarrassed now."
    mc 1 u considerate "\"Well, if there's nothing else you want to do, I guess I should head home for today.\""
    k 1 u smile "\"Thank you again for coming with me, [povFirstName]-san.{nw}"
    show k 1 u worried at fdis
    extend " To be honest, I kinda expected Kiyoshi-san to be... difficult. So... I invited you more to be a kind of... emotional support.\""
    mc 1 u "\"Don't you already have Alex for that?\""
    k 1 u calm "\"Pfft, don't be ridiculous... Alex is the club with which I strike down my enemies.\""
    al "\"I would rather not be swung around like a club, thank you very much.\""
    k 1 u smile "\"You know I didn't mean it literally.\""
    "The wolf shrugs."
    k "\"Anyway, thank you again, [povFirstName]-san.\""
    show k 2 u gentle at fdis
    mc 1 u talk "\"Alright, sure. I'll see you la-\""
    show Black at fdis
    play sound "music/chu.ogg"
    $ renpy.pause(0.4)
    hide Black at fdis
    "Kei-kun quickly leans towards me, giving a quick peck on the cheek."
    mc 1 u considerate "\"Somehow, I kinda already expected you to do that.\""
    k "\"Hehehe.{nw}"
    show k 1 u smile at fdis
    extend " I'll see you tomorrow, [povFirstName]-san.\""
    mc 1 u "\"Tomorrow?\""
    k 1 u "\"Erm... at Urata's volleyball competition, remember?\""
    mc 1 u shock "\"Ah...\""
    "Oh God, I had totally forgotten about that."
    k 1 u sigh "\"... Of course you don't remember. It was stupid of me to think you would.\""
    mc 1 u wince "\"Ahahaha... thanks for reminding me.\""
    k 1 u smile "\"Do try to show up, otherwise he just might murder you.\""
    mc 1 u considerate "\"Will do.\""
    al "\"Have a good afternoon.\""
    mc 1 u smile "\"You too, Alex.\""
    "The two wave at me as I turn around to leave, a goofy smile plastered on my face."
    hide k 1 u smile
    hide al 1 u
    with dissolve
    "Once I'm sure I'm out of sight, I touch the tip of my fingers to the spot on my cheek that Kei-kun kissed."
    "... For some reason, thinking about it again makes me feel like I have butterflies in my stomach and brings a smile to my face."
    stop music2 fadeout 2.5
    stop music3 fadeout 2.5
    $ date = None
    $ schoolfestival = "keisuke"
    jump SchoolFestival
