label Day22_Jun:
    window hide
    scene June5 with fade
    play sound "music/windchimes.ogg"
    show blossoms movie
    $ renpy.pause(5.5)
    scene SGate with fade
    window show
    play sound "music/breeze.ogg"
    $ date = "day22"
    "I yawn, walking through the school gates while lazily rubbing my eyes until they nearly begin to water."
    "I lost track of time last night and ended up staying awake until nearly 3AM watching TV."
    "I was so desperate to distract myself from my own mind that I completely shut down instead."
    "Probably not the healthiest way to deal with my own issues."
    show jin at fdis, three
    show gin at fdis, seven
    with dissolve
    gin "\"Good morning.\""
    "I hear a familiar voice and look up to find two mice had taken to walking on both sides of me at the same time."
    jin "\"What's up?\""
    mc 1 u worried "\"Morning.\""
    "I somehow manage to mutter out a half-hearted greeting in response."
    "Man, I even {i}sound{/i} defeated."
    "The two brothers, hearing the tone of my voice, share a glance with each other, raising their eyebrows."
    gin "\"[povFirstName]-kun, are you okay?\""
    mc 1 u sigh3 "\"Yeah. I'm fine.\""
    gin "\"You know when \"I'm fine\" sounds convincing? When you sigh deeply right before saying it.\""
    mc 1 u worried "\"Just have a lot on my mind. Might not have gotten much sleep either.\""
    play sound "music/tap.ogg"
    "The shorter mouse gives me a friendly pat on the back."
    jin "\"Dude... you're boned.\""
    mc 1 u sigh "\"Huh?\""
    gin "\"How much sleep did you get last night, [povFirstName]-kun?\""
    mc 1 u sigh3 "\"I went to bed at 3AM, why?\""
    jin "\"Yeah, like I said. You're boned. Screwed. Completely f-\""
    mc 1 u sigh "\"Alright alright, I think I get it. You can stop talking now.\""
    "The mouse laughs, sounding incredibly amused with himself."
    gin "\"Uhm... you did remember today is the first day of midterms, right?\""
    stop music
    mc 1 u blank "\"...\""
    "I freeze."
    "The two stop on their tracks a few steps ahead of me once they notice."
    jin "\"Heh. That's not the face of someone who remembered.\""
    "I take a few deep, slow breaths."
    mc 1 u wince "\"... Crap.\""
    "Jin whistles, staring at me with an amazement look on his face."
    jin "\"Wow, you're a lot calmer than I thought you'd be. I was hoping you'd flip out. Kinda underwhelmed, I gotta say.\""
    gin "\"Jin, that's mean. You shouldn't laugh at people's problems.\""
    "The mouse clicks his tongue, looking up at his older brother with a frown."
    jin "\"Jeez, you're such a goody-two shoes.\""
    mc 1 u sigh3 "\"...\""
    "I sigh once again and both heads turn to stare at me at the same time."
    gin "\"Are you going to be alright?\""
    mc 1 u considerate "\"Other than the fact that I didn't study at all and my head isn't even properly in place, yeah.\""
    jin "\"You act like that's a big deal. You never study and you always get good grades anyway. Friggin' annoying.\""
    mc 1 u "\"Well, you certainly are the authority on \"annoying\".\""
    "Jin cocks his head to the side, clearly puzzled by my statement just now."
    "The taller mouse, who've I always known was much brighter than his sibling, covers his mouth with his hand and chuckles."
    jin "\"What does that mean.\""
    mc 1 u smile "\"That it's a good thing you're pretty.\""
    "He puffs his chest with pride, thinking he just got complimented, and cracks a big smile."
    jin "\"Hell yeah I am. Good on you to notice.\""
    show gin at fdis, shake1
    gin "\"Snnrrk...\""
    "Gin snorts, trying to hold back laughter."
    gin "\"I don't think that means what you think it means.\""
    jin "\"Huh? Then what does it mean?\""
    "Gin smiles warmly, putting a hand on his brother's shoulder and slowly shaking his head sideways."
    gin "\"I'll tell you later. Come on, we still need to return some library books before class starts. You're already late on returning it as is.\""
    show jin at fdis, jumping
    jin "\"Oh, right! I had forgotten about that.\""
    "The two start walking away, their torsos turned around so they can look back at me."
    gin "\"See you in class, [povFirstName]-kun.\""
    jin "\"Bye bye, zombie!\""
    "I wave them off as the two rush ahead towards one of the buildings."
    "Zombie, huh? I wonder if I look that bad."
    play music2 "music/BGM/Autumn Day.ogg" fadein 5.0
    play sound "music/slidingdoor.ogg"
    scene SClass with fade
    "I walk into the classroom, going up to my chair and dropping my bag on top of my desk."
    "From a cursory glance around the room, I can see a bunch of students hunched over their desks, desperately flipping pages on their textbooks and notes."
    "Man, midterms really are brutal, huh?"
    "Must be even harder for the people that have to keep on top of those while also studying for college entrance exams."
    "I'm glad I don't have to bother with that sort of thing."
    "However, there is one figure I sort of expected to see here that is sadly still unaccounted for."
    "Jun's desk is still empty, with no sign of him or his belongings."
    "That's weird. He's usually already in school by the time I get here, and I was late today too."
    "Taking another look around, I spot one person that isn't doing what I'd expect them to be doing."
    show ryoji at fdis, five with moveiledis
    "I walk up Ryoji, who is leaning back on his chair and playing some game on his phone."
    "Jeez, he won't drop those even on the day of the exams?"
    play sound "music/disappointment.ogg"
    "Is he really gonna go by with a near failing grade three years in a row?"
    mc 1 u "\"You're already playing games this time of morning?\""
    "He glances up from the screen for only a split second, registering my presence and looking back down to his phone."
    ry "\"Wasn't expecting to get a lecture from you of all people about it.\""
    mc 1 u considerate "\"Nah, no lecture. More like... awe.\""
    ry "\"Oh. Then that's good.\""
    "Yes, I'm in awe of just how irresponsible you can be."
    "... But then again, I did stay up until 3AM watching television so I guess I'm one to talk."
    mc 1 u talk "\"Are you gonna be okay for the test?\""
    ry "\"I'll be fine. There are four midterms every year. It's not the end of the world if I fail on one of them.\""
    play sound "music/disappointment.ogg"
    "There is so much wrong with that I don't even know where to start."
    mc 1 u "\"What game are you playing anyway?\""
    ry "\"Everdark Fantasy.\""
    mc 1 u talk "\"No kidding? Isn't that the gacha game that's been getting really popular lately?\""
    "The bear nods, tapping away at his phone's screen with the same blank look he always has on his face."
    mc 1 u "\"How does this one even work anyway? Like, how do you play?\""
    ry "\"It has a tactical battle system like in the Titan Battle series. The units you get have different skills and stats and fill different roles in battle. You can deploy up to 10 at a time.\""
    mc 1 u confused "\"Huh. That's not what I expected out of a game like this. Actually, that's not what I'd expect you to play at all.\""
    ry "\"There are character quests every time you level up a unit and you can choose a unit to romance at any time.\""
    mc 1 u considerate "\"Ah. Suddenly everything makes so much sense.\""
    "He nods again, his eyes on what appears to be a character's stat screen."
    "I can see the character's portrait, level, and information like health, energy and so much more."
    "To be honest, it looks like a really detailed status screen for basically amounts to a gambling game."
    "... But then again, I don't play these so I don't really know what they're supposed to be like."
    "The gameplay definitely seems to be just like any other tactical RPG I've seen..."
    show ryoji at fdis, three with move
    show j 1 u watch at fdis, seven with moveiridis
    j "\"...\""
    "Before I noticed it, I'm leaning over Ryoji's back, watching his screen with great interest."
    "The bear seems to pick up on this fairly quickly as he leans his phone slightly so I have a better angle of view."
    "Wow, the game even has voice acting when the characters use their special skills too."
    "How fancy!"
    "Oh, and the visual effects are really good too!"
    "Damn, this thing's production values are through the roof!"
    j "\"What are we looking at?\""
    show j 1 u shock at fdis, jumping
    show ryoji at fdis, jumping
    mc 1 u shock "\"Waaaaah!\"" with hpunch
    j 1 u wince "\"J-Jesus. Don't just shout like that, you scared me.\""
    mc 1 u sigh "\"Scared you?! When did you even get here?! Don't sneak up on people like that.\""
    j "\"I sneaked up on you?\""
    ry "\"No, you were fine, Kobayashi. He's the one freaking out.\""
    "Ryoji looks up at me with an uncharacteristic glare, having dropped his phone when I cried out."
    "Luckily it only fell on his table and nothing got broken."
    "Still... oops."
    mc 1 u wince "\"S-sorry.\""
    "Looking around, I see a lot of unfriendly faces pointed at my direction."
    play sound "music/disappointment.ogg"
    "Not a good way to start the day..."
    j 1 u avoid "\"What are you two doing anyway?\""
    mc 1 u talk "\"Huh? Oh. Ryoji was showing me this game he's playing.\""
    j 1 u shock "\"Really? What g- Wait, hang on, we have midterms today. Why are you looking at games?!\""
    mc 1 u considerate "\"Are you really surprised that he'd be playing?\""
    j 1 u sigh "\"I was talking to you.\""
    mc 1 u "\"Me? I'll be fine. Besides, studying ten minutes before the test isn't gonna help anyone anyway.\""
    j 1 u wince "\"I don't know. Wouldn't you want to feel like you did everything you could until the last possible minute. That would at least make me feel better.\""
    mc 1 u considerate "\"That's just for soothing yourself. I don't think it'd help that much.\""
    mc 1 u talk "\"Besides, if you're panicked enough that you feel the need to cram right until the last second, I doubt you'd absorb any of the information you read up on anyway.\""
    j 1 u avoid "\"I guess that's a point.\""
    "Jun's tail lashes behind him, betraying his attempt to look calm."
    mc 1 u confused "\"Are you okay?\""
    j 1 u wince "\"Huh? Me? I think so?\""
    mc 1 u worried "\"...\""
    play sound "music/tap.ogg"
    show j 1 u shock at fdis, five
    show ryoji at offscreenleft
    with move
    "I grab Jun's arm and motion to the back of the classroom with my head, gently pulling him along with me."
    "He looks surprised by the sudden touch but otherwise follows me without an issue."
    mc 1 u worried "\"Are... you sure you're okay?\""
    j 1 u wince "\"Wha- I don't know. I think so?{nw}"
    show j 1 u at fdis
    extend " Why? Are {i}you{/i} okay?\""
    mc 1 u considerate "\"Hmm... that's a complicated question. I didn't sleep much last night.\""
    j 1 u avoid "\"Oh. Because of the exams or...\""
    "He lets the question hang in the air, not finishing his sentence."
    "Whether it is because he doesn't want to or can't I'm not sure."
    mc 1 u considerate "\"I... kinda forgot we had midterms today. Is that bad?\""
    show j 1 u wince at fdis, jumping
    j "\"What? Of course that's bad!\""
    j 1 u avoid "\"Are you sure you're gonna be okay? The last thing I want is for you to get a bad grade because of me.\""
    mc 1 u considerate "\"Meh. I studied about as much as I always do. I should be fine.\""
    j 1 u sigh "\"R-really? That's a relief.\""
    "I pulled him away because I was worried about him and somehow this ended up becoming about me."
    "When did that even happen?"
    mc 1 u "\"Did you study at all?\""
    j 1 u considerate "\"Of course I did.\""
    mc 1 u sigh "\"Outside of the time when I tutored you?\""
    j 1 u wince "\"Y-yeah.\""
    mc 1 u sigh "\"Is that so?\""
    j 1 u avoid "\"Y-yeah. I swear I studied, you don't have to look at me like that.\""
    mc 1 u smile "\"Good boy.\""
    j 1 u sigh "\"Don't call me that.\""
    "Heh. I guess I might have been overthinking things."
    "All things considered, Jun seems to be doing well."
    "... Or at least as well as Jun usually does."
    j 1 u "\"Do you even know what tests we'll have today?\""
    mc 1 u confused "\"Yeah. Don't you? It's on our schedule.\""
    j 1 u shock "\"It is?!\""
    mc 1 u sigh "\"Don't you ever check?\""
    j 1 u think "\"I... think I checked it once right after I started school here. I also think I lost mine a while ago.\""
    mc 1 u sigh3 "\"... You could have just gone to the staff room and asked for another copy to be printed.\""
    j 1 u shock "\"Oh. I didn't know that!\""
    mc 1 u sigh3 "\"I'll go with you to get a new one during lunch break.\""
    show j 1 u happy at fdis, jumping
    j "\"That'd be really helpful, thank you!\""
    mc 1 u "\"For the record, you really need to keep more on top of this kind of stuff.\""
    j 1 u considerate "\"Y-yeah, I know. It's just that my old school was much more lax with our studies so I guess I fell behind when compared to a more prestigious school like this one.\""
    "I really want to know what the hell kind of school he went to before that left him so unprepared for the basic responsibilities you have in high school."
    play sound "music/disappointment.ogg"
    "The Department of Education really ought to take long, hard look at those guys."
    mc 1 u talk "\"I see... And to answer your earlier question, today we'll have Social Studies.\""
    j 1 u "\"Okay, but which ones from Social Studies?\""
    mc 1 u "\"All of them.\""
    show j 1 u cshock at fdis, jumping
    j 1 u "\"All of them?! That's five different subjects!\""
    mc 1 u talk "\"Yeah. Day one is hell day. After that we have two subjects a day which is much more manageable, although each of the subject's tests are much bigger to make up for that.\""
    "The subjects that compose the curriculum can vary a bit from school to school."
    "The really important stuff like Math, Japanese and whatnot are taught at every school."
    "But for stuff like Social Studies, they can be broken down into multiple subjects or taught as just one giant class."
    "For ours, Social Studies is composed of History, Geography, Sociology, Politics and Philosophy."
    j 1 u avoid "\"Oh no... C-can I see your schedule? I wanna know what tests we'll have tomorrow.\""
    mc 1 u "\"Physics and Chemistry on Tuesday. World and Japanese Literature on Wednesday. Math and Health on Thursday. English and Japanese on Friday.\""
    j 1 u shock "\"Wha- You know that off the top of your head?!\""
    mc 1 u smile "\"Sure. It's the same order every year. It follows a pattern too. Social, science, social, science, social.\""
    j 1 u wince "\"Oh man... I wasn't ready to deal with Physics tomorrow already. What am I going to do? I'm just as bad at it as I am at math.\""
    mc 1 u considerate "\"Study right after you get home from school?\""
    j "\"B-but I need to practice too.\""
    mc 1 u "\"What are you talking about? Club activities are paralyzed during midterms. That's why it happens the week after the festival, so there is one big break and then it's back to normal.\""
    j 1 u shock "\"R-really? I didn't know that.\""
    "There is a lot you don't know."
    "It's shocking, honestly."
    j 1 u avoid "\"Do... do you suppose they'd let me use the music room anyway?\""
    "Oh, honey, no."
    mc 1 u considerate "\"Yeah... somehow I don't see that happening.\""
    j "\"Oh...\""
    mc 1 u talk "\"But seriously, if you feel that insecure about physics then you should really head home right after the exams today and study.\""
    j 1 u wince "\"I... I guess.\""
    mc 1 u wry "\"I... can help you if you want.\""
    show j 1 u shockb at fdis, jumping
    j "\"You can?!\""
    mc 1 u talk "\"Sure. I'm used to tutoring people anyway, and I'm sure you already noticed that I'm good at it the last time I helped you with your homework.\""
    j 1 u happy "\"Yeah, that'd be great! You're a lifesaver, [povFirstName]-san!\""
    mc 1 u happy "\"I'm glad you think so.\""
    j 1 u smile "\"Should I come over to your place after school?\""
    mc 1 u talk "\"My place or yours, doesn't really matter to me. Whichever you prefer is fine.\""
    j 1 u considerate "\"You know my parents... I don't think they'd give us room to study. They'd be constantly prodding and wanting to talk to you.\""
    mc 1 u considerate "\"That's... a fair point. Yeah, we'll go to my place after all.\""
    play sound "music/slidingdoor.ogg"
    show j 1 u watch at fdis, three with move
    show shima 1 u smile at fdis, seven with moveiridis
    shima "\"Alright, class. Everyone sit down, and listen.\""
    "Shima-sensei walks into the room from the front door, carrying a pile of papers on his hand which I assume are our tests."
    "The few murmurs of students conversing with each other quickly die down and everyone quickly goes to their desks."
    j 1 u considerate "\"I'll talk to you more later. See you on the other side...\""
    mc 1 u wince "\"Err... okay?\""
    "Why do you sound like you're about to get killed off dramatically? It's just a test."
    "And we sit right next to each other."
    show j 1 u at fdis, offscreenleft
    show shima 1 u smile at fdis, five
    with move
    shima "\"Right, good, everyone's in place. This wasn't as painful as I thought it'd be. Certainly a lot less whining than we got last year.\""
    ky "\"That's because you walked into the classroom blowing an airhorn last year and kept blowing it every time someone opened their mouths to ask a question.\""
    shima "\"Yes, and since the school has since taken my airhorn away and told me that I'm not allowed to blow it in class willy-nilly, we all got to hear your interjection, Nekonishi.\""
    "I see the cat, who sits right at the front of the classroom, shifting on her seat and crossing her arms."
    "Given how quickly her mood can grow sour, I can only imagine she is glaring daggers at Shima-sensei right now."
    shima "\"Here in my hands I hold your fates. I'm sure many of you might say this is an exaggeration but now that you are all seniors, the grades you get are all the more important.\""
    shima "\"Colleges won't take only your results in the entrance exams into account - or at least not the ones that are worth getting into - they'll also check how you did in school, in your extracurriculars, things like that.\""
    shima "\"With that in mind, I believe it goes without saying that any bad grades you get can and will affect you in the future, especially now that we've reached this critical juncture.\""
    shima "\"We'll first start with everyone's favorite subject, History. I'll hand each row a stack of test sheets, everyone take on and pass the stack to the person behind you.\""
    "It's not entirely uncommon for Shima-sensei to rub people the wrong way but man, he becomes even more of a nightmare during exam week."
    "People grumble and groan at the mention of History, probably because no one has fond memories of the subject thanks to Sensei's unorthodox zeal in teaching it."
    shima "\"I trust I don't have to tell anyone to if I catch any student talking, whispering, glancing, gesturing or even {i}thinking{/i} towards, about or with another student, I will take away their tests and give them 0s. Are we understood?\""
    "The entire class nods in understanding, most of everyone now wearing very visible frowns and scowls."
    "I mean, the mood already wasn't the best considering we had tests looming over us but man, the atmosphere here tanked so fast it's surprising."
    "Shima-sensei claps both hands together, looking around to the one final time with a smile."
    shima "\"Very well. You can all get started on your tests then. I wish you all good luck. I really outdid myself when making these so I'm sure you'll need all the luck you can get!\""
    play sound "music/disappointment.ogg"
    "The worst part is this guy really thinks we'd be happy to know that."
    "He doesn't even realize that no one else shares his passion for history."
    "I don't know if that should make me sad or annoyed..."
    stop music2 fadeout 2.5
    scene SClass with fade
    play music "music/crowd01.ogg"
    show hours with dissolve
    $ renpy.pause (1.0)
    hide hours with dissolve
    "The murmurs amongst the students grow more impassioned with each passing second."
    "As soon as the bell rang for lunch break and Shima-sensei left the room, people started talking to each other to compare notes, confirm there answers, among other things."
    "Right after we were done with the history test came both sociology and philosophy."
    "That means after lunch we'll have to deal with politics and geography."
    "I want to say that they saved the worst for last because those classes are a real pain in the ass but... all of social studies is a pain in the ass."
    "I grab my lunchbox from inside my bag, placing it on top of my desk."
    "I turn around to look for a certain tiger and-"
    play music2 "music/BGM/Haretahiha Kousin.ogg" fadein 5.0
    show j 1 u cshock at fdis, five, shake
    "... Find him in what I can only describe as a catatonic state."
    "I would glance sideways every now and again when Shima-sensei wasn't looking to check on how Jun was doing."
    "Which means I got to watch him grow more and more frustrated until I was pretty sure there was smoke coming out of his head as his entire operating system crashed."
    "Man, I really hope he didn't bomb the exams too badly."
    mc 1 u wince "\"Uhm... hey there, Tiger. How are you hanging?\""
    j "\"I-I'm finished. I'm done.\""
    "Oh-oh. Those aren't good first words to hear right after a test."
    mc 1 u considerate "\"H-here. How about you put your table together with mine and we go over our answers while we eat, okay?\""
    j 1 u sigh "\"... Okay.\""
    play sound "music/chairscoot.ogg"
    show j 1 u avoid at fdis
    "Man, he looks really frazzled."
    "Poor guy must be freaking out by now."
    mc 1 u wry "\"Come on, Jun. Cheer up. I'm sure you can't have done {i}that{/i} badly.\""
    j 1 u sigh "\"You heard Shima-sensei. I don't have the luxury of doing badly {i}at all{/i}. I might as well kiss my schooling in Europe goodbye if I do.\""
    mc 1 u considerate "\"There's no point in freaking out so soon. You studied hard, right? The fruits of your labor are bound to show themselves.\""
    j 1 u avoid "\"... You really think so?\""
    mc 1 u smile "\"Of course. Provided you really did study. You're not stupid, Jun. You're just... really easily distracted.\""
    play sound "music/stab.ogg"
    show j 1 u wince at fdis, shake1
    stop music2 fadeout 2.5
    j "\"Guh...\""
    "I give him an encouraging pat on the back, trying my best to cheer him up."
    "If he crashes and burns before the day is over then he'll really be screwed with the next two tests."
    "I don't know when it was that I took the responsibility of managing his tension but I'll still do my best."
    mc 1 u talk "\"Come on, let's eat. You can't think on an empty stomach. Take out your lunch.\""
    j 1 u considerate "\"Yeah, you're right.\""
    play music2 "music/BGM/Little by Little I Walk.ogg" fadein 5.0
    "At least he follows my instructions diligently enough."
    "I suppose he doesn't have enough fight left in him to go against what I say."
    "I'm not sure if that's a good or a bad thing."
    j 1 u avoid "\"Do you think Shoichi-san and Mizoguchi-san will be coming over today?\""
    mc 1 u "\"Hard to say. They might be going over their answers with their classmates too. Goofing off too much during lunch break isn't a good idea, otherwise you might end up getting distracted.\""
    j 1 u wince "\"Yeah, I guess...\""
    mc 1 u smile "\"Of course, that doesn't mean a little levity isn't welcome. There's only so much stress your mind can take before it snaps. Here, you want a sausage octopus?\""
    j 1 u shockb "\"Oooh, thank you!\""
    "Heh, at least he's easy to distract with treats."
    mc 1 u "\"What did you bring for lunch anyway?\""
    j 1 u smile "\"Egg salad, rice and curry.\""
    mc 1 u talk "\"Oh wow, that sounds pretty good.\""
    j 1 u watch "\"Yours has a lot more stuff than mine though.\""
    mc 1 u considerate "\"Yeah. I... might have gone a little overboard when cooking yesterday because I wanted to distract myself.\""
    "I brought some pork cutlet, rice, sausages shaped like octopi, omelet, salt and vinegar roasted veggies, potato pure and katsudon."
    "Mine and Aki's lunchboxes ended up being a weird mix of Japanese and western food... not that I mind."
    "It definitely felt like a brick inside my back with how densely I packed it though."
    mc 1 u smile "\"I don't think I'll be able to eat all of this so feel free to have some too.\""
    j 1 u happy "\"Thank you, I appreciate it. Your cooking is really good, [povFirstName]-san!\""
    mc 1 u smile "\"Heh. I'm happy you think so.\""
    mc 1 u talk "\"Actually, can I try some of yours? I don't think I've ever eaten your cooking before.\""
    j 1 u smile "\"Sure. Have some.\""
    "He lightly pushes his lunchbox towards me and I grab a little bit of curry and rice."
    mc 1 u talk "\"Woah, this is really good. Surprisingly mild too. Kinda sweet.\""
    j 1 u happy "\"Yeah. I like to add a little more mirin than one might usually. I like the sweetness.\""
    mc 1 u considerate "\"Jeez, you're a sweet tooth even when it comes to lunch?\""
    j 1 u considerate "\"Hehe... guilty.\""
    mc 1 u talk "\"Well, I'm not gonna complain because it's really good but you should really watch your sugar intake.\""
    j 1 u wince "\"N-noted.\""
    "I playfully rib him whenever given the opportunity."
    "It gives him a chance to bounce back and I can tell it's already improving his spirits quite a bit."
    "Honestly, I'm relieved."
    "Last thing I want is for him to be sulking all day, especially if we go over to my place to study afterwards."
    "There's just no way we could make any progress if he's sobbing and freaking out the whole time."
    mc 1 u "\"By the way, do you already know when your next piano competition is going to be?\""
    j 1 u watch "\"Hmm? Yeah, I have the schedule for the entire year. I know it off the top of my head too.\""
    play sound "music/disappointment.ogg"
    "Really? And yet you don't have the school's schedule? What kind of backwards sense of priorities is that?"
    "At least put equal effort into both things, you're not gonna be able to go to Germany if you flunk out of school in favor of doing well in the piano competitions."
    j 1 u smile "\"The next competition is going to be next month, though I'll have to travel for that one again. Actually, out of the next four competitions, only the last one will be in town.\""
    mc 1 u talk "\"That sounds like a hassle to deal with.\""
    j 1 u considerate "\"I don't have much choice. I need results to make up for my time away. I told you this before.\""
    mc 1 u talk "\"Fair enough. Do you have a new setlist for the next competition?\""
    j 1 u watch "\"I do.\""
    "..."
    mc 1 u sigh "\"And?\""
    j 1 u think "\"And it's good.\""
    mc 1 u sigh2 "\"Sure, but what are the songs?\""
    j 1 u bored "\"What's the point of telling you? Every time I try you just complain saying there's no point since you have no idea what I'm talking about.\""
    play sound "music/stab.ogg"
    "!" with hpunch
    mc 1 u considerate "\"Hehehe... I guess that's true.\""
    show j 1 u watch at fdis
    mc 1 u smile "\"Well, how about you show it to me instead? Saaaay, this weekend after we're done with exams?\""
    j 1 u shockb "\"...\""
    "Jun's entire face suddenly lights up for some reason."
    "Me saying something so innocuous seems to have cheered him up more than any amount of encouragement I could have attempted."
    j "\"R-really? I'd love to play for you again!\""
    mc 1 u fsmile "\"W-wow, I didn't think you'd be that happy to do it.\""
    j 1 u happyb "\"No no, I love it when you watch me. It really relaxes me whenever you're around. It helps me stay calm even when I'm stressing over the sheet music.\""
    mc 1 u shockb "\"I-is that so?\""
    "... I don't know how I'm supposed to take this."
    "My face feels hot all of a sudden..."
    j 1 u smile "\"Although if you want me to play for you then you're going to have to come over to my place.\""
    mc 1 u smile "\"Sounds like a plan.\""
    j 1 u happyb "\"Hehehe.\""
    "Jun's tail slowly sways back and forth behind him."
    "He's such an open book, his body language is so easy to read."
    "It's kinda refreshing being around someone like him."
    "It's really easy to tell when he's genuinely happy or excited which takes a load off of my shoulders since I don't have to constantly second guess his intentions or feelings."
    "And even in the rare situations where he's lying, it's always painfully obvious that he's lying."
    "... Although that part can be a little annoying when I know he's lying and can't tell what about."
    "Actually, scratch that, supper annoying when that happens but still."
    j 1 u watch "\"So, [povFirstName]-san, can we talk about our answers to the tests? We've already been chatting for a while and haven't gotten to it yet. {size=-2}Most of my lunch is gone too.{/size}\""
    mc 1 u talk "\"Oh, sure. Let's start with history then.\""
    stop music fadeout 2.5
    stop music2 fadeout 2.5
    play music3 "music/BGM/Dog Days.ogg" fadein 5.0
    scene SGate
    show j 1 u sigh at fdis, five
    with fade
    show hours with dissolve
    $ renpy.pause (1.0)
    hide hours with dissolve
    "We're finally done with our tests for the day."
    "Jun is trudging along beside me with a sour expression and sagging shoulders."
    "He hasn't said a word after the test for anything other than complaining about it."
    "I think the only time he went quiet is when we went to the staff room to ask for a new schedule copy for him."
    "Probably because he didn't want to start bitching in earshot of the teachers."
    j "\"How was I supposed to know what year Nobunaga Oda died. I barely even know who he is in the first place.\""
    mc 1 u considerate "\"Come on, that was a middle school level test question right there. Shima-sensei only put it there so he could say he was fulfilling the curriculum.\""
    "Since he barely gave any classes on Japanese History like he was supposed to, he just threw us a few super easy questions that everyone should know instead to say he did his job."
    "I wasn't about to complain about it since I got all of them right and they were easy points but Jun didn't seem all too pleased about it."
    "In fact, he'd been bitching about every single one of the tests, getting more and more vocal about it as we continued on this topic."
    "I've been trying to go unresponsive and just let him tire himself out but it seems he doesn't even require my presence here to continue on with his rant."
    j 1 u bored "\"That's easy for you to say. You probably did really well.\""
    mc 1 u considerate "\"That's neither here nor there.\""
    j "\"It's still true though.\""
    mc 1 u wry "\"We won't know that until we get the results back.\""
    "He grumbles under his breath, clearly dissatisfied with my answer."
    "My guess is he wants me to engage him so he can keep venting."
    "I don't want to sound like a jerk but that's really annoying and I don't want to deal with that."
    show s 1 u at fdis, seven
    show j 1 u bored at fdis, three
    with move
    "From the corner of my eye, I catch the figure of a husky walking past the school gates, his steps faster enough than ours that he passes us by without saying a word."
    "It's easy to notice someone that big walking by after all."
    mc 1 u talk "\"Hey, Shoichi!\""
    "I call out to him and he promptly stops on his tracks."
    j 1 u happy "\"Ah, Shoichi-san, I didn't see you there!\""
    "Jun runs the few steps between us, dodging through the other students passing by on their way and stopping right next to Shoichi."
    show s 1 u blank at fdis
    "Shoichi turns around very slowly, the look on his face catching me by surprise and making me freeze."
    s "\"Do you two need me for something?\""
    show j 1 u shock at fdis
    "Jun's face goes a few shades whiter and he stares up at Shoichi slack-jawed."
    mc 1 u shock "\"Woah, is everything okay?\""
    s "\"Yes.\""
    j 1 u wince "\"R-really? It doesn't look like it. You seem to be in a bad mood, Shoichi-san.\""
    s 1 u displeased "\"...\""
    "He looks down between the two of us, almost as if he were studying us."
    "His eyes stop on Jun for just a moment."
    "I could swear he frowned a little too."
    show s 1 u sigh at fdis
    "But just as quickly as the frown came, Shoichi's whole face relaxes and he lets out a loud sigh."
    "I was ready to chalk it up to my imagination but Jun's whole body shivers lightly right next to me and he looks... really put off."
    s "\"I'm just tired. Today hasn't exactly been relaxing.\""
    j 1 u avoid "\"... Oh, I see. D-did you have a hard time with the tests too?\""
    s 1 u avoid "\"Among other things. I just have a lot on my plate right now.\""
    mc 1 u worried "\"Do you need any help? We were heading home so I could tutor Jun but if we can help-\""
    s 1 u worried "\"Sorry, not this time, [povFirstName]. Right now I just need to get home and lie down for a few hours.\""
    j "\"A-are you sure? We could go out, maybe try to have some fun? We could invite Mizoguchi-san and Keisuke-san too.\""
    s 1 u blank "\"Sorry, I'll pass. Besides, I should really get home and study. I can't afford to let my grades fall. And I'm pretty sure Saya and Urushihara will be doing the same.\""
    s 1 u considerate "\"Good luck with your studies, you two. And [povFirstName]... take care of Jun-kun, alright?\""
    mc 1 u shock "\"Huh? I- Uhm- Sure.\""
    show s 1 u considerate at fdis, offscreenleft with moveoledis
    show j 1 u shock at fdis, five with move
    "The two of us stare shocked as Shoichi walks away without another word."
    "Did something happen?"
    "He seemed fine the last time we talked."
    "... Was it something I said?"
    j 1 u avoid "\"...\""
    "Jun visibly deflates next to me, all the excitement he had mustered when we saw Shoichi quickly left like a deflating air balloon."
    mc 1 u wince "\"... What just happened?\""
    j "\"...\""
    mc 1 u considerate "\"Come on, you're not gonna get down too, are you?\""
    j "\"I wonder...\""
    mc 1 u talk "\"Wonder what?\""
    j 1 u shock "\"Huh?!\""
    j 1 u wince "\"Oh, uhm, nothing. It's nothing. We should probably get going.\""
    mc 1 u sigh "\"Come on, are you hiding something from me now?\""
    j 1 u considerate "\"N-not hiding. Just not sure what to think.\""
    mc 1 u sigh "\"...\""
    show j 1 u avoid at fdis
    "I examine his face long and hard, trying to decide whether I believe him or not."
    "Jun is such a crappy liar that it's really easy to see when he's hiding something but, at the same time, he's so stubborn about his secrets that trying to pry it out might just land me in trouble."
    mc 1 u sigh3 "\"Alright. I'm gonna let this go for now because I just want to get home and help you study.\""
    j 1 u considerate "\"Thank you.\""
    mc 1 u "\"Oh no, don't you thank me yet. I am still going to give you a drill sargeant level of tutoring so you better be prepared.\""
    show j 1 u wince at fdis, shake1
    play sound "music/stab.ogg"
    j "\"R-roger that.\""
    "Maybe tormenting him a little bit might help me deal with my frustration..."
    stop music3 fadeout 2.5
    play music2 "music/BGM/Snowy Day.ogg" fadein 5.0
    scene BedroomE
    show j 1 c watch at fdis, five
    with fade
    show sometime with dissolve
    $ renpy.pause (1.0)
    hide sometime with dissolve
    "It doesn't take us long to get home."
    "Jun insisted we stop by his place so he could get changed, saying he didn't want to be in his uniform all afternoon."
    "Not that I blame him for that."
    "I just waited outside his place while he got changed and then we headed to my house."
    play sound "music/fan.ogg"
    "One of the first things I do when I get here is turn on the fan."
    "The weather was pretty okay earlier in the morning but as the afternoon sun rolled around, it started getting more and more overbearing."
    j 1 c happy "\"Haah, cool air feels so nice.\""
    mc 1 u smile "\"It certainly does.\""
    "Jun leans with his face towards the fan and I watch for a little bit as his fur blows along with the wind."
    "I find myself smiling for some reason at the sight, somehow feeling relaxed just looking at it."
    j 1 c smile "\"Do you want to get changed too, [povFirstName]-san? I can wait outside.\""
    mc 1 u "\"If you were going to ask me that as soon as you walked into the room then why did you even walk in in the first place?\""
    j 1 c wince "\"I- Uhm... I didn't really think about it.\""
    mc 1 u smile "\"You're fine. I don't mind staying in my uniform. Besides, it helps me focus.\""
    j 1 c watch "\"Focus?\""
    mc 1 u "\"Yeah. If I get changed then I'm just gonna want to sit down and laze around and that just sounds like a terrible idea.\""
    mc 1 u smile "\"So I'll keep my uniform on while I need to be productive.\""
    j 1 c happy "\"Oh, I guess I understand!\""
    "Not that it is a very complicated concept but..."
    show j 1 c watch at fdis
    mc 1 u "\"Soooo, where do you want to start?\""
    j 1 c think "\"...\""
    "Jun starts fiddling with his bag, opening it up and checking his notebooks inside, probably trying to think of how to tackle the situation."
    j 1 c watch "\"What tests do we have tomorrow?\""
    mc 1 u considerate "\"Can't you just look at your schedule? We just got you a new one not an hour ago.\""
    j 1 c shock "\"Oh, right!\""
    "He fishes the folded sheet of paper from inside his back and starts looking it up."
    "I'll admit that I could just tell him the answer but he really should get into the habit of looking things up on his own."
    "... Not least of all because I don't want to end up turning into his personal search engine."
    j 1 c think "\"Let's see, we have...{nw}"
    show j 1 c wince at fdis
    extend " Right, physics and chemistry.\""
    mc 1 u talk "\"Exactly. I know you said you weren't doing too well on physics but what about chemistry.\""
    j 1 c considerate "\"I'm... doing better than in physics.\""
    mc 1 u sigh "\"That doesn't exactly fill me with confidence.\""
    j 1 c avoid "\"Sorry... my studies are a mess.\""
    mc 1 u considerate "\"Well... not much you can do about it. Honestly, I feel a bit bad. I promised a while ago that I'd start tutoring you but I've barely done anything to help.\""
    j 1 c shock "\"What? No no no, it's my fault.\""
    j 1 c wince "\"I was all too happy to let you forget because I really didn't want to do it.\""
    j 1 c sigh "\"Besides, I barely even study when I have the time. I just start playing videogames or something.\""
    "Heh. Guess he really is a kindred spirit."
    "... Although I suppose I at least do the bare minimum."
    mc 1 u smile "\"Alright! No good will come out of lamenting so we better get started. Since you're doing worst in physics let's start from there. Once we go through the whole material at least once we can check chemistry out.\""
    j 1 c happy "\"Okay!\""
    stop music2 fadeout 2.5
    play music "music/night.ogg" fadein 5.0
    scene SkyN with fade
    play sound "music/scribbling.ogg"
    show hours with dissolve
    $ renpy.pause (1.0)
    hide hours with dissolve
    "I absentmindedly stare out the window, watching the sky darken with each passing minute, the typical quiet that comes with nighttime ringing a near deafening silence in my ears."
    "The irregular sounds of Jun's scribbling on the paper is the only thing breaking the monotony."
    "Every now and again he'd ask me questions, but for the most part once we went over stuff a couple times he started getting the hang of things."
    "Now he's just doing some exercises to work things out on his own and make sure the lessons are sticking properly to his mind."
    "He's not stupid like he sometimes says he is, he's just unbelievably lazy."
    "Getting through to him is a chore, but a decent amount reminders of what's on the line managed to get him sufficiently motivated."
    "... And also pretty anxious."
    "Fingers crossed and hope things work out for the best."
    "I feel something poking my shoulder and look to my side."
    play music2 "music/BGM/I Want to Know You - Narration.ogg" fadein 5.0
    scene BedroomN
    show j 1 c considerate at fdis, five
    with fade
    show hours with dissolve
    $ renpy.pause(1.0)
    hide hours with dissolve
    "I see Jun's face sitting alarmingly close to mine, making me flinch for a brief second."
    "Luckily he doesn't notice that."
    "The tiger is holding out a piece of paper and sighing, his left brow twitching a few times."
    j "\"I think I'm done...\""
    "Man, it almost feels like someone sucked his soul out of him, he's so listless."
    mc 1 u smile "\"Really? All of them? Damn, that was faster than I thought.\""
    j 1 c avoid "\"[povFirstName]-san, that was cruel. You gave me more exercises to work out than we had on our tests.\""
    mc 1 u "\"Nah. You only think that because the social studies tests are shorter. These are about the same size you'll get tomorrow.\""
    j 1 c sigh "\"Then that's even more cruel. What if I'm too fatigued tomorrow to focus on the tests?\""
    mc 1 u smile "\"Just make sure you get a proper night's sleep and you be fine.\""
    j 1 c avoid "\"Yeah, sure...\""
    "He slumps forward, resting his chin and part of his chest on the table."
    "His eyes flit towards me every now and again while I go over his work, checking them against the answer keys and grading his \"paper\"."
    mc 1 u talk "\"{size=-4}Hmm... I see.{/size}\""
    "He might have been complaining about it but he did a decent job."
    "I mean... for only a single day's worth of studying."
    "We're not gonna get any miracles out of him but against the other test I made him take when he first started, this is a marked improvement."
    j "\"Well?\""
    play sound "music/pageflip.ogg"
    mc 1 u smile "\"Yeah, you did better.\""
    j 1 c wince "\"By how much?\""
    mc 1 u talk "\"You got 74\% correct.\""
    j 1 c sigh "\"No waaay, only 74? That's not enough to pass!\""
    mc 1 u considerate "\"I mean, sure, but that's still nearly 30 points more than before. It's something you can actually make up for in the next tests.\""
    j 1 c avoid "\"I know that, but... I can't just be making up for my grades. I need to do my best if I want to go where I want to go.\""
    "The big cat sighs and whines, his chin still smushed up against my table."
    "The fur around his neck is somewhat bristled and even his tail is lashing behind him."
    "Not only that, with the way his brow is furrowed, it's making a somewhat comical fold right on the center of his forehead."
    j 1 c wince "\"[povFirstName]-san, is something the matter?\""
    mc 1 u talk "\"What?\""
    j 1 c considerate "\"You're staring really hard at my face. Is something wrong?\""
    mc 1 u wince "\"I am?\""
    j 1 c think2 "\"Yeah. You didn't notice?\""
    mc 1 u talk "\"Sorry, I was lost in thought. I didn't even notice I was staring.\""
    j 1 c considerate "\"And you say I'm bad with that sort of thing.\""
    mc 1 u considerate "\"Hehe...\""
    "Jun sighs, grabbing his paper and going over it once again."
    "He taps the tip of his pen idly against the desk at a regular interval, almost as if he were marking a rhythm."
    j 1 c wince "\"Can we go again?\""
    mc 1 u considerate "\"You sure? It's already kind of late and we've been at this for a while. I don't want you burning yourself out.\""
    j 1 c avoid "\"Oh... would it be too much of a bother for you?\""
    mc 1 u shock "\"No no, that's not what I meant.\""
    mc 1 u considerate "\"I just don't want you to push yourself too much. It might have the opposite effect if you're too tired to focus on the test tomorrow.\""
    j 1 c considerate "\"Isn't that what I said just now?\""
    mc 1 u considerate "\"Well... on second thought, it had a lot of merit.\""
    j 1 c avoid "\"... Can we keep going anyway? Please? I feel like if I review where I went wrong one last time I might be able to get it right.\""
    mc 1 u avoid "\"Alright, alright. Just... remember that if you want to do well on the midterms, you're gonna have to keep doing this every day for the rest of the week.\""
    mc 1 u considerate "\"Just because you can handle it today doesn't mean you can last a week.\""
    j 1 c pout "\"I can handle it.\""
    mc 1 u happy "\"Haha, sure you can.\""
    mc 1 u smile "\"Alright, see this question right here? You were doing it right at firsts but-\""
    show j 1 c think2 at fdis
    "Jun leans up against me, listening very attentively to my explanations."
    "I wish he could apply himself like this more often."
    "I don't think he'd turn into a model student but at least he'd be getting passing grades."
    j 1 c shock "\"Oh, so I have to do it like this?\""
    "I have to explain things more than once but he does eventually catch on and fixes his mistakes."
    show j 1 c watch at fdis
    "Before I even notice it, Jun has scuttled so close to me with his chair that he's nearly leaning his body against the left side of mine while going over my explanations."
    "Only a small distance separates us right now."
    "This is probably the closest we've ever been..."
    "... And yet, I don't really mind it."
    "It's a pleasant, calm feeling."
    "It's nothing grand or exciting, but it still leaves me feeling... peaceful."
    "I don't really know how to explain it very well."
    "Slowly, I lift my left arm until my hand is just a hair's width away from his head."
    "I want so badly to reach out and do... something."
    "But I don't know what."
    "I'm afraid of trying to do something and messing things up."
    "Those complicated thoughts I had the past few days just come rushing back in with full force."
    "I'm okay with being friends. I'm okay with being close."
    "... But just thinking of making a jump to a romantic relationship scares me."
    "It makes me anxious, it makes it hard to breathe."
    "Just to think of how badly I could screw things up and how he could come out hurt."
    "I don't want that... the fear is so bad it's paralyzing."
    "But I still want to reach out to him..."
    "I want to close this gap between us, even just a little bit."
    "Even if it's not much, I want to feel like I'm walking forward."
    "I asked for time to think things over and I've been obsessing about it since yesterday."
    "... But if I want anything to happen, if I want to come to any kind of decision, then shouldn't I take a first step towards it? Even if it's scary?"
    "I hesitantly begin to pet him, going very slowly and trying not to make a big deal out of it."
    "Jun does make any noises or sudden movements."
    "In fact, I at first think he doesn't even realize it."
    "But then, almost as if it were nothing, the few centimeters that separated us are quickly closed and the tiger leans his head to rest on my shoulder."
    "Calmly and naturally, he continues to listen to my now shaky voice."
    "Calmly and naturally, his tail swishes behind him, getting a little bit closer at time, until it's wrapped tightly around my waist and the tip is resting on top of my right thigh."
    "He's... I'll give it to him, he's surprisingly smooth."
    "I catch my voice faltering and I get the urge to look away."
    "But I know if I make a big deal about this, it'll probably affect him too."
    "For good or bad, he's a pretty empathic person and if I let the atmosphere get awkward, he's gonna feel it even more than I will."
    mc 1 u considerate "\"Erm... s-so, does it all make sense so far?\""
    "I somehow manage to go through all the questions he got wrong with only minimal stuttering."
    j 1 c happy "\"Yeah, it does. At least I think it does.\""
    j 1 c smile "\"Can you make another exam sheet for me?\""
    mc 1 u considerate "\"Sure. Just give me a few minutes.\""
    j 1 c happy "\"Okay. I'm gonna go over these again while you do that.\""
    "He pulls away from, leaning forward onto the desk while he goes back to work, redoing the questions he got wrong."
    "I take a deep, breath right afterwards, sighing."
    "I'm conflicted... I feel both relieved and disappointed right now..."
    "Why can't I just understand what I want?"
    mc 1 u considerate "\"Just keep in mind this is the last one. After you're done with it we should really wrap up for the day. It's already nearly 9PM and you need to sleep.\""
    j 1 c smile "\"Okay. No problem.\""
    stop music2 fadeout 2.5
    stop music fadeout 2.5
    $ date = None
    jump Day23_Jun
