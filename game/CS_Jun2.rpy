label junstart_1:
    $ uihide = True
    stop music2 fadeout 2.5
    $ junhearts += 1
    scene SCorridor with fade
    "Another day comes by where I have absolutely nothing to do."
    "Man, I hate not having the courts operational."
    "Just after I had gotten fired up to practice more..."
    "Still, I have to find some way to entertain myself."
    "If I were to go home now, no doubt I'd end up bored out of my mind there."
    "Mom is definitely going to be busy with work and Aki is going to be at his own club."
    "It's stuff like this that makes me miss being in a private club..."
    "Ugh... I don't want to be thinking about this right now... I just need a distraction..."
    "Which is exactly why I decided to come {i}here{/i}!"
    scene MusicRoom with fade
    play music2 "music/BGM/Little by Little I Walk.ogg" fadein 5.0
    play sound "music/slidingdoor.ogg"
    mc 1 u happy "\"Pardon my intru-\""
    "Wait, there's someone here with Jun?"
    show j 1 u shock at fdis, three
    show so 1 u smile at seven
    with dissolve
    j "\"Ah, [povFirstName]-san. You came here today too?\""
    mc 1 u curious "\"Well, yeah. I did ask if it was okay for me to drop by every now and then last time I was here... Is this a bad time?\""
    show j 1 u gentle at fdis
    j "\"No no, someone else just dropped by to see me before you came here!\""
    show j 1 u smile at fdis
    "I look at the guy next to Jun."
    "He's a panda, which I guess is kinda unusual."
    "Pandas aren't all that common in Japan. They mostly live in China and the neighboring regions."
    "... Well, it's not like they're super rare here either."
    "Especially in later years. Immigration has done a nice job of getting a lot of different species that weren't common in Japan to show up around here."
    show j 1 u happy at fdis
    j "\"[povFirstName]-san, this is Sora. He's my next-door neighbor!\""
    show j 1 u watch at fdis
    mc 1 u curious "\"Your neighbor?\""
    "The bear turns to me."
    "From the get go, I can see that he's avoiding eye contact."
    "Don't tell me he's the standoff-ish type..."
    so "\"H-hi... I'm Sora Kimura... {size=-2}nice to meet you and stuff...{/size}\""
    mc 1 u fsmile "\"Uhm... hi, Sora...-kun. I'm [povName]. I'm one of Jun's classmates.\""
    "The panda nods, his eyes still glued to the floor."
    so "\"Y-yeah, I know who you are. {size=-2}You're pretty famous here in this school and all...{/size}\""
    mc 1 u considerate "\"I-is that so?\""
    "Awkwaaaaard!"
    "Seriously, that was the most robotic introduction I've ever seen."
    "I can't tell whether this guy has an attitude problem or if he's just avoiding me."
    show j 1 u considerate at fdis
    j "\"Uhm... Sora-kun, you should really try to at least look at him while you talk...\""
    so "\"Ah...\""
    "As soon as he was called out on it, the panda lifted his gaze from the floor and focused his eyes on me."
    "I could immediately make out a reddish tint on his cheeks as he did so."
    show j 1 u wry at fdis
    j "\"Sorry, [povFirstName]-san. He's a bit shy...\""
    "Shy? This whole thing was because he was shy?!"
    mc 1 u shock "\"Is that so?\""
    "Could have fooled me."
    "The bear looks away from me again, his cheeks turning even redder."
    so "\"S-sorry... I'm not good... at talking to people...\""
    mc 1 u smile "\"It's alright. This sort of thing is a lot more common than you think.\""
    mc 1 u laugh "\"Still... Man, I really thought you were some sort of delinquent.\""
    show so 1 u smile at shake1
    play sound "music/stab.ogg"
    show j 1 u wince at fdis
    j "\"[povFirstName]-san!\""
    mc 1 u curious "\"What?\""
    show j 1 u sigh at fdis
    j "\"Behave yourself!\""
    mc 1 u shockb "\"H-huh?!\""
    "What did I do?!"
    show j 1 u considerate at fdis
    j "\"I'm sorry, Sora-kun. [povFirstName]-san can be a little dense sometimes...\""
    mc 1 u dismay "\"W-what? I'm dense?\""
    "Seriously, what did I do?!"
    so "\"It's alright... I guess I had that one coming.\""
    show j 1 u annoyed at fdis
    j "\"[povFirstName]-san, you say that you thought someone was a delinquent because he was having a hard time talking to you. You just made him more self-conscious!\""
    mc 1 u shock "\"O-oh, really?! I'm sorry, I totally didn't mean to.\""
    "Jeez, I'm not used to being lectured by Jun of all people."
    so "\"Like I said, it's alright.\""
    "I'm glad he's not mad at me about it..."
    mc 1 u considerate "\"I guess I really am a little dense sometimes, huh?\""
    show j 1 u think at fdis
    j "\"No kidding.\""
    mc 1 u dismay "\"Don't agree with me so easily!\""
    show j 1 u watch at fdis
    j "\"Then don't say something so agreeable.\""
    mc 1 u sigh2 "\"... Alright, I'll let it go this time.\""
    show j 1 u think at fdis
    j "\"As if you had a choice.\""
    show j 1 u watch at fdis
    mc 1 u sigh "\"Okay, who are you and what have you done to the normally meek Jun?\""
    show j 1 u happy at fdis
    j "\"Hehehe.\""
    "Don't just try to laugh me off!"
    "I swear, he's getting more and more comfortable with me as time passes by."
    "... I'm no longer sure that's a good thing."
    "Ugh... I'll just let that go for now."
    mc 1 u curious "\"By the way, Sora...-kun, I've never seen you around school before. Are you another transfer student like Jun or are you a freshman?\""
    so "\"The latter. Although I'm also new in town. My family only moved here from Hokkaido a few months ago.\""
    mc 1 u smile "\"Oh, is that so? How was it like in Hokkaido?\""
    so "\"There was a lot of snow... I kinda miss it. Most of all, I miss the ice hockey.\""
    mc 1 u confused "\"Ice hockey? What's that?\""
    show j 1 u wince at fdis
    j "\"You seriously don't know what ice hockey is?\""
    mc 1 u "\"Not really. I don't pay much attention to other sports... or, well, I at least assume it's a sport. Is it like Olympic skating?\""
    so "\"No, it's... uhm... how can I even explain ice hockey to someone who's never heard about it?\""
    show j 1 u sigh at fdis
    j "\"I wouldn't even bother with it.\""
    mc 1 u pout "\"Hey, that's mean!\""
    show j 1 u think at fdis
    j "\"Just being honest. If you don't pay attention to other sports in the first place, why would you pay attention when he tried to explain hockey to you?\""
    so "\"Oh, I hadn't thought of that!\""
    "... I hate the fact that he's right."
    mc 1 u sigh "\"Yeah yeah. I'll let it slide this time, smartass.\""
    show j 1 u smile at fdis
    j "\"What? I'm just telling the truth!\""
    mc 1 u avoid "\"If you say so.\""
    mc 1 u "\"By the way, Sora-kun, if you don't mind me asking, why did your family move here in the first place? Saitama is not exactly the closest place to Hokkaido.\""
    so "\"My parents wanted me to get into a good high school so I'd have a better chance at college. Since Saitama has one of the best high schools {i}and{/i} one of the best colleges in the country, they decided this place would make more sense to move to.\""
    mc 1 u think "\"I guess that makes sense... although I think the warmer climate can't have been easy to get used to.\""
    so "\"Yeah, tell me about it. It was a real bitch over here. I still feel like I'm wearing too many clothes when I come to school...\""
    mc 1 u considerate "\"Oh, yeah... our school is pretty rigid with uniform over here. You're only allowed to drop the blazer in the summer...\""
    so "\"It's not just the blazer. If it were up to me, I'd be walking around shirtless. That's how hot this place is to me.\""
    mc 1 u wince "\"Oh, ouch, that can't be good.\""
    mc 1 u confused "\"Wait a minute... aren't our classrooms air-conditioned?\""
    show j 1 u considerate at fdis
    j "\"Yeah, about that..."
    show j 1 u wry at fdis
    extend " The air-conditioning system in Sora-kun's classroom broke two weeks ago. They still haven't gotten around to fixing it.\""
    mc 1 u worried "\"Oh... oh God... I think I understand your pain now.\""
    "The panda sighs. Upon a closer look, I realize that his fur is very matted... no doubt from sweat."
    so "\"You don't... you really, really don't.\""
    mc 1 u considerate "\"... Sorry.\""
    "Poor fella..."
    so "\"Anyway, as much as I like the air-conditioning in this room, I was asked to run some errands after class. I only came by to give you the package.\""
    show j 1 u think at fdis
    j "\"Oh, so you're going already?\""
    so "\"Yeah, otherwise I'm gonna end up being late.\""
    show j 1 u smile at fdis
    j "\"Okay then. Thanks again, Sora-kun!\""
    so "\"Don't mention it.\""
    "He turns to me, his eyes immediately darting to the floor."
    "How come he's so comfortable with Jun and so nervous around me?"
    so "\"Uhm... it was a pleasure meeting you, [povFirstName]-senpai...\""
    mc 1 u considerate "\"S-senpai? You really don't have to call me that...\""
    "Why can't you just call me -san like you do with Jun?"
    show j 1 u sigh at fdis
    j "\"It's true. [povFirstName]-san is nowhere near reliable enough to be called that.\""
    mc 1 u avoid "\"Do you have a grudge against me or something?\""
    show j 1 u watch at fdis
    j "\"Huh? No, why?\""
    "... The fact that he can criticize me so innocently really scares me."
    mc 1 u sigh2 "\"Forget it...\""
    mc 1 u smile "\"Anyway, it was nice meeting you, Sora-kun.\""
    "Hearing my words, the panda's eyes finally lift to meet with mine, flashing me a bright smile."
    so "\"Yeah, it was!\""
    show so 1 u smile at offscreenright with moveoridis
    show j 1 u watch at fdis, five with move
    "With those words said, the bear walks out of the classroom without a glance back."
    mc 1 u talk "\"... That guy really is something.\""
    j 1 u happy "\"Yeah, Sora-kun really is a nice guy!\""
    "Not where I was going with this but I guess I'll accept it."
    mc 1 u curious "\"By the way, what's this package that he came by to drop for you?\""
    show j 1 u watch at fdis
    j "\"Huh? Oh, we were talking about some games and Sora-kun offered to lend me his Playsystem Viva. There was a game in it that I really wanted to play but I didn't have the money to buy the console.\""
    mc 1 u shock "\"Wow, that's awfully nice of him!\""
    show j 1 u happy at fdis
    j "\"Like I said, he's a really nice guy.\""
    "Huh... this goes to show that you can never trust first impressions."
    "Wait... this reminds me, didn't I promise to lend Jun one of my consoles a while back?"
    "Jeez, I'm really forgetful with this kind of thing..."
    mc 1 u "\"What game was it that you wanted to play?\""
    show j 1 u watch at fdis
    j "\"The new Monster Tamer game."
    show j 1 u smile at fdis
    extend " I heard that it's the best one ever released so I was really dying to play.\""
    mc 1 u think "\"Huh... I've never played that franchise before. Is it any good?\""
    show j 1 u watch at fdis
    j "\"If you like hardcore equipment grinding and multiplayer gaming.\""
    mc 1 u avoid "\"... Pass.\""
    show j 1 u happy at fdis
    j "\"Too bad. It could have been fun if the two of us played together.\""
    mc 1 u considerate "\"I'm not really into mandatory grinding.\""
    "... Or any other form of grinding for that matter."
    "This is the main reason I could never get into MMOs..."
    show j 1 u watch at fdis
    j "\"Anyway, what brought you here today, [povFirstName]-san?\""
    mc 1 u smile "\"Me? Nothing much. I just felt like chatting for a bit.\""
    show j 1 u happy at fdis
    j "\"Did you want to watch me play again?\""
    mc 1 u happy "\"Well, I'm not gonna lie, that was one of the reasons I came by. I really enjoyed watching you last time.\""
    j "\"Hehehe, thanks. I really enjoyed playing for you too.\""
    "Talk about an earnest guy."
    mc 1 u smile "\"How have you been feeling though? You're not pushing yourself, are you?\""
    show j 1 u wince at fdis
    j "\"This conversation again? I thought we had already gone over this...\""
    mc 1 u considerate "\"We have, I just wanted to check up on you.\""
    show j 1 u at fdis
    j "\"I'm fine. I was fine last time and I'm still fine today. You don't have to worry so much...\""
    show j 1 u sigh at fdis
    j "\"Seriously, don't... it's annoying.\""
    mc 1 u considerate "\"Message received...\""
    "And here I thought I could probe him about it without upsetting him."
    "I guess Jun really hates having people fuss over him like that..."
    mc 1 u "\By the way, do you have any idea what that Akutagawa guy is going to be playing in the next competition?\""
    show j 1 u think at fdis
    j "\"No. The list of participants and their selected pieces won't be released until a few days before the competition.\""
    mc 1 u think "\"I thought this kind of info was usually released at least a month before...\""
    show j 1 u watch at fdis
    j "\"Usually but not always. It depends on what company is organizing the competition, who their sponsors are, etc etc.\""
    show j 1 u considerate at fdis
    j "\"There actually isn't as much of a standard as one would expect. A lot of the rules are completely dependent on the organizers.\""
    mc 1 u wince "\"That's not very encouraging...\""
    show j 1 u sigh at fdis
    j "\"I know. It's just something you've got to learn to deal with early in your career.\""
    show j 1 u smile at fdis
    j "\"On the bright side, it also means that players can pick and choose what competitions they sign up for based on which sets of regulations they feel more comfortable with.\""
    show j 1 u considerate at fdis
    j "\"Not that {i}I{/i} have the luxury of choosing, of course. I just have to sign up for everything that comes my way and pray to my lucky stars that I win.\""
    show j 1 u think at fdis
    j "\"But it's not really that bad. Regulations can vary from what clothes you're allowed to wear to what songs you're allowed to choose, whether or not you can have an accompanist playing another instrument. Things like that.\""
    show j 1 u watch at fdis
    mc 1 u "\"What about this one. What are the rules for it?\""
    show j 1 u think at fdis
    j "\"There isn't much regulation when it comes to clothing other than the statement that all participants must be \"adequately\" clothed, which by itself is pretty vague.\""
    show j 1 u watch at fdis
    j "\"Set lists can include any songs from a list of pre-approved composers whose names I won't even bother reciting to you.\""
    show j 1 u think at fdis
    j "\"The only real regulation when it comes to songs is that you have to pick a piano solo, so accompanists are very much not allowed.\""
    show j 1 u considerate at fdis
    j "\"To be fair, this is pretty much as close as you can get to a standard when it comes to rules for competitions.\""
    mc 1 u wince "\"It doesn't sound like it leaves much room for individuality though.\""
    show j 1 u wry at fdis
    j "\"It's like I told you before, competitions aren't really the place for that sort of thing."
    show j 1 u think at fdis
    extend " Players who want to showcase their individuality would have a better time taking part in a recital.\""
    mc 1 u smile "\"Have you ever taken part in a piano recital?\""
    show j 1 u considerate at fdis
    j "\"Once... it was quite a few years ago.\""
    mc 1 u curious "\"Why don't you do it again?\""
    show j 1 u wince at fdis
    j "\"I'd like to but..."
    show j 1 u considerate at fdis
    extend " That sort of thing is impossible for someone like me...\""
    mc 1 u curious "\"Someone like you? What does that even mean?\""
    show j 1 u avoid at fdis
    j "\"That's... can we just change the subject?\""
    "He's hiding something from me... and not even doing a good job of it."
    "I don't know if I should probe him about it... he'd probably just get mad at me."
    $ pressjun = 0
    menu:
        "..."
        "Ask him.":
            $ pressjun += 1
            "Then again, this isn't the first time he's said something on that same vein."
            "In fact, I remember him once saying that he gave up on becoming a pianist for... reasons."
            "Now I wonder... could that reason have anything to do with this?"
            mc 1 u "\"Come on, just tell me what the reason is. Is it really that big of a deal.\""
            show j 1 u wince at fdis
            j "\"I... it's a private matter, okay?\""
            mc 1 u curious "\"So is it like a family problem? Did your parents not agree with it or something?\""
            j "\"No, that's not-\""
            mc 1 u think "\"Then is it something wrong with you? Was the pressure too much for you or maybe yo-\""
            show j 1 u annoyed at fdis
            j "\"{size=+4}[povFirstName]-san!{/size}\"" with hpunch
            mc 1 u shock "\"?!\""
            "Did... did Jun just yell?"
            j "\"I already told you I don't want to talk about it. Either drop it or get out!\""
            mc 1 u shock "\"...\""
            "W-what? Now he's kicking me out?"
            mc 1 u considerate "\"C-come on now, Jun. It's just a harmless question. Is it really that big a d-\""
            j "\"It's not about the question, it's about you consistently ignoring me when I said I don't want to talk about it!\""
            j "\"If you're not going to respect my boundaries then please just leave.\""
            "I... I don't think I've ever seen Jun get angry like this before."
            "Is it really that big of a deal?"
            "Ugh... I should just apologize and defuse this situation..."
            mc 1 u worried "\"I... I'm sorry. I shouldn't have pried.\""
            j "\"..."
            show j 1 u sigh at fdis
            extend " Alright, you're forgiven... this time.\""
            show j 1 u annoyed at fdis
            j "\"But I'm being serious here. If I tell you that I don't want to talk about it, don't keep asking like some sort of clueless moron!\""
            mc 1 u considerate "\"M-message received. I'll be more careful in the future.\""
            "Jeez, he's a bit scary when he's mad."
            "In the future, I should probably wait for a better opportunity before asking a question like that."
            "... I totally didn't learn my lesson, huh?"
        "Stay quiet.":
            "No, that'd be a bad idea."
            "He's already gotten annoyed at me once for probing him about his welfare. I doubt he'd be very receptive to me ignoring his requests to not talk about something."
            "... Ugh... I really want to know about it though."
            "The downsides of being curious..."
            "... Actually, now that I think of it, is there even any {i}upside{/i} to it?"
            mc 1 u sigh "\"... You really suck at hiding things, you know?\""
            show j 1 u shock at fdis
            j "\"Huh?\""
            mc 1 u sigh2 "\"I mean... it's plain as day that you're hiding {i}something{/i}.\""
            mc 1 u smile "\"But it's alright. I'll let it slide. You'll tell me when you're ready to tell me.\""
            j "\"..."
            show j 1 u happy at fdis
            extend " Thanks, [povFirstName]-san!\""
            mc 1 u happy "\"Don't get used to it, though. I'm doing it just this one time.\""
            show j 1 u smile at fdis
            j "\"I'm still not gonna tell.\""
            mc 1 u smile "\"Aww, how cruel.\""
            show j 1 u happy at fdis
            j "\"Hehehe.\""
            "I wonder if there'll ever be a good time for me to ask about it..."
    stop music fadeout 2.5
    stop music2 fadeout 2.5
    $ date = None
    jump cs_final
